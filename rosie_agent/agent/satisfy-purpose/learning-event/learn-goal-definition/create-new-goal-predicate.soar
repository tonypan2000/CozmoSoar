sp {learn-goal-definition*propose*create-new-goal-predicate*unary
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^type unary
           ^id.general-arg-rep <pred>
           ^{1}.general-arg-rep <obj1>
          -^added)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-new-goal-predicate
        ^predicate-info <info>)
}

sp {learn-goal-definition*propose*create-new-goal-predicate*relation
   (state <s> ^name learn-goal-definition
              ^goal-predicate-info <info>)
   (<info> ^type relation
           ^id.general-arg-rep <pred>
           ^{1}.general-arg-rep <obj1>
           ^{2}.general-arg-rep <obj2>
          -^added)
-->
   (<s> ^operator <o> + =)
   (<o> ^name create-new-goal-predicate
        ^predicate-info <info>)
}

sp {learn-goal-definition*apply*create-new-goal-predicate*copy*predicate*structure*unary
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^goal-structure <goal>)
   (<o> ^name create-new-goal-predicate
        ^predicate-info <info>)
   (<info> ^type unary
           ^id.general-arg-rep <pred>
           ^{1}.general-arg-rep <obj1>)
-->
   (<goal> ^predicate <new-pred>)
   (<new-pred> ^type unary ^id <pred> ^1 <obj1>)
   (<info> ^added true)
}

sp {learn-goal-definition*apply*create-new-goal-predicate*copy*predicate*structure*relation
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^goal-structure <goal>)
   (<o> ^name create-new-goal-predicate
        ^predicate-info <info>)
   (<info> ^type relation
           ^id.general-arg-rep <pred>
           ^{1}.general-arg-rep <obj1>
           ^{2}.general-arg-rep <obj2>)
-->
   (<goal> ^predicate <new-pred>)
   (<new-pred> ^type relation ^id <pred> ^1 <obj1> ^2 <obj2>)
   (<info> ^added true)
}
