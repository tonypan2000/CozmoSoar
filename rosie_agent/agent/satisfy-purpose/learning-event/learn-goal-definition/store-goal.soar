sp {learn-goal-definition*propose*store-goal
   (state <s> ^name learn-goal-definition
              ^goal-structure <goal>
              ^generalized-goal <gpreds>
             -^stored <gpred>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name store-goal
        ^goal-structure <goal>
        ^goal-predicates <gpreds>)
}

sp {learn-goal-definition*apply*store-goal*add*goal-structure*to*tcn
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^task-concept-network <tcn>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-structure <goal>)
   (<tcn> -^goal)
-->
   (<tcn> ^goal <goal>)
   (<cmd> ^store <tcn>)
}

sp {learn-goal-definition*apply*store-goal*create*subgoal
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^task-concept-network.goal <goal>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates <gpreds>)
   (<goal> -^subgoal-count)
-->
   (<goal> ^subgoal-count 1
           ^1 <subgoal1>)
   (<subgoal1> ^handle (make-constant-symbol |tasksubgoal|))
   (<cmd> ^store <goal> <subgoal1>)
}

sp {learn-goal-definition*apply*store-goal*add*predicates
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^task-concept-network.goal.{1} <subgoal1>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates <gpreds>)
-->
   (<subgoal1> ^predicates <gpreds>)
   (<cmd> ^store <subgoal1> <gpreds>)
   (<s> ^stored <gpreds>)
}

sp {learn-goal-definition*apply*store-goal*predicate
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates.{ <pred-num> << 1 2 3 4 5 >> } <pred>)
-->
   (<cmd> ^store <pred>)
}

sp {learn-goal-definition*apply*store-goal*predicate*arg
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates.<pred-num>.{ << id 1 2 3 4 5 >> } <arg>)
-->
   (<cmd> ^store <arg>)
}

sp {learn-goal-definition*apply*store-goal*predicate*arg*default
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates.<pred-num>.{ << id 1 2 3 4 5 >> }.default <def>)
-->
   (<cmd> ^store <def>)
}

sp {learn-goal-definition*apply*store-goal*predicate*arg*imagined
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates.<pred-num>.{ << id 1 2 3 4 5 >> }.imagined <img>)
-->
   (<cmd> ^store <img>)
}

sp {learn-goal-definition*apply*store-goal*conditions
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates.conditions <conds>)
-->
   (<cmd> ^store <conds>)
}

sp {learn-goal-definition*apply*store-goal*condition*predicate
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates.conditions.{ << 1 2 3 4 5 >> } <pred>)
-->
   (<cmd> ^store <pred>)
}

sp {learn-goal-definition*apply*store-goal*condition*predicate*arg
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates.conditions.<pred-num>.{ << id 1 2 3 4 5 >> } <arg>)
-->
   (<cmd> ^store <arg>)
}

sp {learn-goal-definition*apply*store-goal*condition*predicate*arg*default
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates.conditions.<pred-num>.{ << id 1 2 3 4 5 >> }.default <def>)
-->
   (<cmd> ^store <def>)
}

sp {learn-goal-definition*apply*store-goal*condition*predicate*arg*imagined
   (state <s> ^name learn-goal-definition
              ^operator <o>
              ^smem.command <cmd>)
   (<o> ^name store-goal
        ^goal-predicates.conditions.<pred-num>.{ << id 1 2 3 4 5 >> }.imagined <img>)
-->
   (<cmd> ^store <img>)
}
