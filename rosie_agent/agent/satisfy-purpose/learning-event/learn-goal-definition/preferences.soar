sp {learn-goal-definition*prefer*create-goal-structure*over*create*predicates
   (state <s> ^name learn-goal-definition
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name create-goal-structure)
   (<o2> ^name << create-new-goal-predicate update-goal-predicate >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {learn-goal-definition*prefer*add-goal-predicate*over*store
   (state <s> ^name learn-goal-definition
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << create-new-goal-predicate update-goal-predicate >>)
   (<o2> ^name store-goal)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {learn-goal-definition*prefer*update*over*create*goal-predicate
   (state <s> ^name learn-goal-definition
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name update-goal-predicate)
   (<o2> ^name create-new-goal-predicate)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {learn-goal-definition*update-goal-predicate*prefer*better*matches
   (state <s> ^name learn-goal-definition
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name update-goal-predicate
         ^match <m1>)
   (<o2> ^name update-goal-predicate
         ^match < <m1>)
-->
   (<s> ^operator <o1> > <o2>)
}
   
sp {learn-goal-definition*prefer*smem*operators
   (state <s> ^name learn-goal-definition
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name << smem-query >>)
   (<o2> ^name << create-goal-structure create-new-goal-predicate update-goal-predicate store-goal >>)
-->
   (<s> ^operator <o1> > <o2>)
}

sp {learn-goal-definition*indifference*generalize-goal*generalize-argument
   (state <s> ^name learn-goal-definition
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name generalize-task-goal)
   (<o2> ^name generalize-task-argument)
-->
   (<s> ^operator <o1> = <o2>)
}
