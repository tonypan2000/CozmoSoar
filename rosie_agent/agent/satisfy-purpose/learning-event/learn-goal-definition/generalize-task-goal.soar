sp {learn-goal-definition*elaborate*generalize-task-goal
   (state <s> ^name learn-goal-definition
              ^task-operator <task-op>
              ^task-concept-network.procedural <tcn-proc>)
-->
   (<s> ^generalize-task-goal <gen-goal>)
   (<gen-goal> ^reference-task-op <task-op>
               ^reference-tcn-proc <tcn-proc>)
}

sp {learn-goal-definition*generalize-task-goal*elaborate*generalize-task-argument*object*reference-info
   (state <s> ^name generalize-task-goal
              ^superstate.name learn-goal-definition
              ^generalize-task-argument <arg>
              ^superstate.reference-info <info>)
   (<arg> ^arg-type object
          ^instantiation <i>)
   (<i> ^id <obj>)
   (<info> ^referent <obj>)
-->
   (<i> ^reference-info <info>)
}

# copy the result of the generalization process
sp {learn-goal-definition*elaborate*generalized-goal*from*generalize-task-goal
  (state <s> ^name learn-goal-definition
             ^generalize-task-goal.general-goal-rep <rep>)
-->
  (<s> ^generalized-goal <rep>)
}

# ENUMERATE: 1-5 goal predicates
sp {learn-goal-definition*elaborate*generalize-task-goal*task-goal*1*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-goal <gen-goal>)
   (<o> ^goal-predicate <p1>
       -^goal-predicate { <p2> <> <p1> })
-->
   (<gen-goal> ^task-goal <goal>)
   (<goal> ^pred-count 1
           ^1 <p1>)
}

sp {learn-goal-definition*elaborate*generalize-task-goal*task-goal*2*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-goal <gen-goal>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
       -^goal-predicate { <p3> <> <p2> <> <p1> })
-->
   (<gen-goal> ^task-goal <goal>)
   (<goal> ^pred-count 2
           ^1 <p1>
           ^2 <p2>)
}

sp {learn-goal-definition*elaborate*generalize-task-goal*task-goal*3*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-goal <gen-goal>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
        ^goal-predicate { <p3> > <p2> }
       -^goal-predicate { <p4> <> <p3> <> <p2> <> <p1> })
-->
   (<gen-goal> ^task-goal <goal>)
   (<goal> ^pred-count 3
           ^1 <p1>
           ^2 <p2>
           ^3 <p3>)
}

sp {learn-goal-definition*elaborate*generalize-task-goal*task-goal*4*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-goal <gen-goal>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
        ^goal-predicate { <p3> > <p2> }
        ^goal-predicate { <p4> > <p3> }
       -^goal-predicate { <p5> <> <p4> <> <p3> <> <p2> <> <p1> })
-->
   (<gen-goal> ^task-goal <goal>)
   (<goal> ^pred-count 4
           ^1 <p1>
           ^2 <p2>
           ^3 <p3>
           ^4 <p4>)
}

sp {learn-goal-definition*elaborate*generalize-task-goal*task-goal*5*predicates
   (state <s> ^name learn-goal-definition
              ^superstate.operator <o>
              ^generalize-task-goal <gen-goal>)
   (<o> ^goal-predicate <p1>
        ^goal-predicate { <p2> > <p1> }
        ^goal-predicate { <p3> > <p2> }
        ^goal-predicate { <p4> > <p3> }
        ^goal-predicate { <p5> > <p4> }
       -^goal-predicate { <p6> <> <p5> <> <p4> <> <p3> <> <p2> <> <p1> })
-->
   (<gen-goal> ^task-goal <goal>)
   (<goal> ^pred-count 5
           ^1 <p1>
           ^2 <p2>
           ^3 <p3>
           ^4 <p4>
           ^5 <p5>)
}
