######################################################
#
# Propose: init-agent
#
######################################################

sp {top-state*propose*init-agent
   (state <s> ^top-state <s>
             -^name rosie)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name init-agent)
}

#######################################################
# 
# Propose: interaction 
#
########################################################

sp {top-state*propose*interaction*status-change
   (state <s> ^top-state <s>
             -^agent-params.parser-test true
              ^interaction.status.<any> <status>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interaction
        ^type status-change
        ^status-info <status>
        ^status-type <any>)
}

sp {top-state*propose*interaction*interaction-change
   (state <s> ^top-state <s>
             -^agent-params.parser-test true
              ^interaction.changes.<type> <change>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name interaction
        ^type interaction-change
        ^change-info <change>
        ^change-type <type>)
}

#######################################################
# 
# Propose: push task segment
#
########################################################

sp {top-state*propose*push-task-segment*task-operator
   (state <s> ^superstate nil
              ^task-stack <stack>)
   (<stack> ^top none
            ^queued-tasks.task <qtask>)
   (<qtask> ^order <order>
            ^task-operator <task-op>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name push-task-segment
        ^order <order>
        ^segment <qtask>)
}

sp {top-state*apply*execute-task*remove*queued-task
	(state <s> ^superstate nil
               ^operator <o>
               ^task-stack.queued-tasks <queue>)
 (<o> ^name execute-task
      ^task-segment.task-operator <task-op>)
 (<queue> ^task <qtask>)
 (<qtask> ^task-operator <task-op>)
-->
(<queue> ^task <qtask> -)        
}

sp {top-state*prefer*push-task-segment*lower*order
   (state <s> ^superstate nil
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name push-task-segment
         ^order <ord1>)
   (<o2> ^name push-task-segment
         ^order > <ord1>)
-->
   (<s> ^operator <o1> > <o2>)
}

#######################################################
# 
# Propose: satisfy-purpose
#
########################################################

sp {top-state*propose*satisfy-purpose
   (state <s> ^top-state <s>
             #-^parser-testing true
		      ^interaction.stack.segment.purpose {<p> <> none})
   (<p> -^type << action-command execute-task goal-description >>
	-^category goal)
-->
   (<s> ^operator <o> +)
   (<o> ^name satisfy-purpose
        ^purpose <p>)
}

sp {prefer*execute-task*over*satisfy-purpose*finished-game-learning
  (state <s> ^superstate nil
              ^interaction.stack.segment.purpose.type << finished-game-learning >>
              ^operator <o1> +
              ^operator <o2> +)
  (<o1> ^name execute-task)
  (<o2> ^name satisfy-purpose)
-->
  (<s> ^operator <o1> > <o2>)
}

#JK stop actions from occur when running internal
#turn off to allow writting
#sp {prefer*satisfy-purpose*over*execute-task*finished-game-learning*internal
#  (state <s> ^superstate nil
#             ^agent-params.domain internal
#             ^interaction.stack.segment.purpose.type << finished-game-learning >>
#              ^operator <o1> +
#              ^operator <o2> +)
#  (<o2> ^name << execute-task manage-action-stack >>)
#  (<o1> ^name satisfy-purpose)
#-->
#  (<s> ^operator <o1> > <o2>)
#}

sp {top-state*propose*execute-task
   (state <s> ^superstate nil
              ^interaction.stack.segment.purpose.type << finished-game-learning execute-task >>
              ^task-stack.bottom { <seg> <> none })
-->
   (<s> ^operator <o> +)
   (<o> ^name execute-task
        ^task-segment <seg>)
}

sp {top-state*propose*execute-task*empty*interaction
   (state <s> ^superstate nil
              ^interaction.stack.segment.purpose none
              ^task-stack.bottom { <seg> <> none })
-->
   (<s> ^operator <o> +)
   (<o> ^name execute-task
        ^task-segment <seg>)
}

### Will take a command message and create a new task segment with it
sp {top-state*propose*handle-new-task-command
   (state <s> ^superstate nil
              ^interaction.stack.segment.purpose <p>)
   (<p> ^type execute-task
        ^parameters <params>)
   (<params> ^command-message <msg>)
-->
   (<s> ^operator <o> +)
   (<o> ^name handle-new-task-command
        ^command-message <msg>)
}

#sp {top-state*propose*learning*action-type
#   (state <s> ^top-state <s>
#				^interaction.stack.segment.purpose <p>)
#   (<p> ^satisfaction.learning-event)
#   (<p> ^type << action-command >>)
#-->
#   (<s> ^operator <o> +)
#   (<o> ^name action-learning
#        ^purpose <p>)
#}
#
#sp {top-state*propose*learning*goal
#   (state <s> ^top-state <s>
#			 ^interaction.stack.segment.purpose <p>)
#   (<p> ^satisfaction.learning-event)
#   (<p> ^category goal)
#-->
#   (<s> ^operator <o> +)
#   (<o> ^name action-learning
#        ^purpose <p>)
#}
#
#sp {top-state*propose*lookup-preposition
#   (state <s> ^prepositions.prep.name <name>
#              ^superstate nil
#              ^top-state.robot-state.arm.action wait
#             -^found-prep.type <name>)
#-->
#   (<s> ^operator <o> + =)
#   (<o> ^name lookup-preposition
#        ^type <name>)
#}
#
