#################### ask-for-goal ######################
# Parameters:
#    task-operator - the task-operator representing the task
#                      for which we are asking for the goal

# Push a new segment to get the goal from the instructor
sp {anystate*apply*initiate-interaction*ask-for-goal*push*segment
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type ask-for-goal
        ^task-operator <aop>)
-->
   (<chgs> ^new-segment-info <info>)
   (<info> ^purpose <p>
           ^originator agent)
   (<p> ^type get-goal-info
        ^parameters.task-operator <aop>
        ^satisfaction.dialog-event <de>)
   (<de> ^type goal-description
         ^originator instructor)
}

# Send an outgoing message to ask for the goal
sp {anystate*apply*initiate-interaction*ask-for-goal*send*message*get-goal-info
   (state <s> ^operator <o>
              ^quiescence t
              ^top-state.interaction.changes <chgs>)
   (<o> ^name initiate-interaction
        ^type ask-for-goal)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type get-goal-info
          ^complete-message true
          ^fields <f>)
}