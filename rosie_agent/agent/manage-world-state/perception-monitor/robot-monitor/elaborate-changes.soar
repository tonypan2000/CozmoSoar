################################################################
# Change: changed-arm-moving-status
#   Occurs when the arm.moving-status on the input-link changes
################################################################

sp {top-state*perception-monitor*change-info*elaborate*changed-arm-moving-status
   (state <s> ^top-state <s>
              ^perception-monitor.change-info <info>)
-->
   (<info> ^change <changed-arm-moving-status>)
   (<changed-arm-moving-status> ^type changed-arm-moving-status
                         ^category robot
                         ^op-name attend-to-changed-arm-moving-status)
}

sp {top-state*perception-monitor*robot-monitor*elaborate*change*changed-arm-moving-status
   (state <s> ^top-state <s>
              ^perception-monitor.changes <changes>
              ^perception-monitor.input-link.self <self>
              ^world.robot.arm <arm>)
   (<self> ^arm.moving-status <cur-status>)
   (<arm> -^moving-status <cur-status>)
-->
   (<changes> ^change <change>)
   (<change> ^type changed-arm-moving-status
             ^self-info <self>)
}

#########################################################
# Change: changed-moving-status
#   Occurs when the robot moving-status changes on the input-link
#########################################################

sp {top-state*perception-monitor*change-info*elaborate*changed-moving-status
   (state <s> ^top-state <s>
              ^perception-monitor.change-info <info>)
-->
   (<info> ^change <changed-moving-status>)
   (<changed-moving-status> ^type changed-moving-status
                           ^category robot
                           ^op-name attend-to-changed-moving-status)
}

sp {top-state*perception-monitor*robot-monitor*elaborate*change*changed-moving-status
   (state <s> ^top-state <s>
              ^perception-monitor.input-link.self <self>
              ^world.robot <robot>
              ^perception-monitor.changes <changes>)
   (<self> ^moving-status <cur-status>)
   (<robot> -^moving-status <cur-status>)
-->
   (<changes> ^change <change>)
   (<change> ^type changed-moving-status
             ^self-info <self>)
}

########################################################
# Change: changed-waypoint
#    Occurs when the robot's current waypoint changes
#      on the input-link
########################################################

sp {top-state*perception-monitor*change-info*elaborate*changed-waypoint
   (state <s> ^top-state <s>
              ^perception-monitor.change-info <info>)
-->
   (<info> ^change <changed-waypoint>)
   (<changed-waypoint> ^type changed-waypoint
                       ^category robot
                       ^op-name attend-to-changed-waypoint)
}

sp {top-state*perception-monitor*robot-monitor*elaborate*change*new-waypoint
   (state <s> ^top-state <s>
              ^world.robot <robot>
              ^perception-monitor.input-link.self.current-waypoint.waypoint-handle <wp-handle>
              ^perception-monitor.changes <changes>)
   (<robot> -^current-waypoint.handle <wp-handle>)
-->
   (<changes> ^change <change>)
   (<change> ^type changed-waypoint
             ^new-waypoint-handle <wp-handle>)
}
