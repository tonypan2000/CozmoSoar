# object-monitor.object-info.differences <diffs>
#   Contains information about the differences between the perception information 
#      and the belief state information (changes in pos/volume/etc)

# Elaborate the differences identifier
sp {top-state*perception-monitor*object-monitor*object-info*elaborate*differences
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.{ << object-info new-object-info >> } <obj-info>)
-->
   (<obj-info> ^differences <diffs>)
}

###################################################
#
# position differences
#   compare-position <compare> - svs filter
#   pos-diff <diff> - svs filter result 
#   has-moved true - true if pos-diff > threshold
#
###################################################

sp {top-state*perception-monitor*object-monitor*elaborate*svs-command*compare-position
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^svs.command <cmd>)
   (<obj-info> ^perception-obj.id <perc-id>
               ^belief-obj.id <bel-id>
               ^differences <diffs>)
-->
   (<cmd> ^extract <compare>)
   (<compare> ^type distance 
              ^a <a>
              ^b <b>
              ^distance_type centroid)
   (<a> ^type node
        ^id <perc-id>)
   (<b> ^type node
        ^id <bel-id>)
   (<diffs> ^compare-position <compare>)
}

# Note: with magicbot, we compare the object's position to the robot (we don't perceive it when holding it)
sp {top-state*perception-monitor*object-monitor*elaborate*svs-command*compare-position*grabbed*magicbot
   (state <s> ^superstate nil
              ^agent-params.domain << cozmo magicbot >>
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^svs.command <cmd>)
   (<obj-info> -^perception-obj
               ^belief-obj.id <bel-id>
               ^wm-obj.predicates.arm-status grabbed
               ^differences <diffs>)
-->
   (<cmd> ^extract <compare>)
   (<compare> ^type distance 
              ^a <a>
              ^b <b>
              ^distance_type centroid)
   (<a> ^type node
        ^id robot)
   (<b> ^type node
        ^id <bel-id>)
   (<diffs> ^compare-position <compare>)
}

sp {top-state*perception-monitor*object-monitor*object-info*differences*elaborate*pos-diff
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info.differences <diffs>)
   (<diffs> ^compare-position.result.record.value <val>)
-->
   (<diffs> ^pos-diff <val>)
}

sp {top-state*perception-monitor*object-monitor*object-info*differences*elaborate*has-moved
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info.differences <diffs>
              ^agent-params.pos-diff-threshold <thresh>)
   (<diffs> ^pos-diff > <thresh>)
-->
   (<diffs> ^has-moved true)
}

##################################################################
#
# Monitoring change in volumes
#   belief-vol <vol> - filter to extract belief-obj volume
#   perception-vol <vol> - filter to extract perception-obj volume
#   vol-diff <diff> - perception-vol/belief-vol
#   has-grown true - true if vol-diff > vol-high-diff-threshold
#   has-shrunk true - true if vol-diff < vol-low-diff-threshold
#   growth-wait true - true if not yet time recheck-growth-at <t> 
#
##################################################################

sp {top-state*perception-monitor*object-monitor*elaborate*svs-command*compare-volume
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info <obj-info>
              ^svs.command <cmd>)
   (<obj-info> ^perception-obj.id <perc-id>
               ^belief-obj.id <bel-id>
               ^differences <diffs>)
-->
   (<cmd> ^extract <perc-vol> <bel-vol>)
   (<perc-vol> ^type volume
               ^a <pa>
               ^volume_type scale)
   (<pa> ^type node
         ^id <perc-id>)
   (<bel-vol> ^type volume
              ^a <ba>
              ^volume_type scale)
   (<ba> ^type node
         ^id <bel-id>)
   (<diffs> ^belief-vol <bel-vol>
            ^perception-vol <perc-vol>)
}

sp {top-state*perception-monitor*object-monitor*object-info*differences*elaborate*vol-diff
   (state <s> ^superstate nil 
              ^perception-monitor.object-monitor.object-info.differences <diffs>)
   (<diffs> ^belief-vol.result.record.value <bel-vol>
            ^perception-vol.result.record.value <perc-vol>)
-->
   # Add a cubic mm to each to avoid divide by 0
   (<diffs> ^vol-diff (/ (+ <perc-vol> 0.000000001) (+ <bel-vol> 0.000000001)))
}

sp {top-state*perception-monitor*object-monitor*object-info*differences*elaborate*has-shrunk
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info.differences <diffs>
              ^agent-params.vol-low-diff-threshold <low>)
   (<diffs> ^vol-diff < <low>)
-->
   (<diffs> ^has-shrunk true)
}

sp {top-state*perception-monitor*object-monitor*object-info*differences*elaborate*has-grown
   (state <s> ^superstate nil
              ^perception-monitor.object-monitor.object-info.differences <diffs>
              ^agent-params.vol-high-diff-threshold <high>)
   (<diffs> ^vol-diff > <high>)
-->
   (<diffs> ^has-grown true)
}
