sp {top-state*perception-monitor*elaborate*change*info
   (state <s> ^superstate nil
              ^perception-monitor <perc-mon>)
   (<perc-mon> ^changes.change <change>
               ^change-info.change <info>)
   (<change> ^type <change-type>)
   (<info> ^type <change-type>)
-->
   (<change> ^info <info>)
}

sp {top-state*perception-monitor*internal*simulation*elaborate*object
   (state <s> ^superstate nil
              ^agent-params.domain internal
              ^agent-params.simulate-perception true
              ^perception-monitor <pmon>
              ^internal-world.objects.object <obj>)
   (<pmon> ^input-link <il>
           ^spatial-scene <scene>)
   (<il> ^self.current-waypoint.waypoint-handle <wph>
         ^objects <objs>)
   (<obj> ^handle <h>
          ^waypoint <wph>)
-->
   (<scene> ^child <child>)
   (<child> ^id <h>
            ^object-source perception)
   (<objs> ^object <il-obj>)
   (<il-obj> ^object-handle <h>)
}

sp {top-state*perception-monitor*internal*simulation*elaborate*object*predicates
   (state <s> ^superstate nil
              ^agent-params.domain internal
              ^agent-params.simulate-perception true
              ^perception-monitor.input-link.objects.object <il-obj>
              ^internal-world.objects.object <obj>)
   (<obj> ^handle <h>
          ^predicates.<prop> <pred>)
   (<il-obj> ^object-handle <h>)
-->
   (<il-obj> ^property <il-prop>)
   (<il-prop> ^property-handle <prop>
              ^type visual
              ^values <vals>)
   (<vals> ^<pred> 1.0)
}
