# Elaborate current location 
sp {top-state*world*object*predicates*elaborate*modifier1*current1
   (state <s> ^top-state <s>
              ^world.robot.current-location <loc>)
   (<loc> ^predicates <preds>)
-->
   (<preds> ^modifier1 current1)
}

# Elaborate the pointed to object
sp {top-state*world*object*predicates*elaborate*indicated*pointed
   (state <s> ^superstate nil
              ^perception-monitor.input-link.pointed-object <h>
              ^world.objects.object <obj>)
   (<obj> ^handle <h>
          ^predicates <preds>)
-->
   (<preds> ^indicated pointed)
}

sp {top-state*world*object*predicates*elaborate*category*block
   (state <s> ^superstate nil
              ^agent-params.domain << tabletop fetch >>
              ^world.objects.object.predicates <preds>)
   (<preds> -^name -^category location -^category object)
-->
   (<preds> ^category block)
}

sp {top-state*world*object*predicates*elaborate*category*location
   (state <s> ^superstate nil
              ^agent-params.domain << tabletop fetch >>
              ^world.objects.object.predicates <preds>)
   (<preds> ^name <name> -^category object)
-->
   (<preds> ^category location)
}

sp {top-state*world*object*predicates*elaborate*not-grabbed*internal
   (state <s> ^superstate nil
              ^agent-params.domain internal
             -^agent-params.simulate-perception true
              ^world.objects.object.predicates <preds>)
   (<preds> ^category block)
-->
   (<preds> ^is-grabbable1 grabbable1
            ^arm-status not-grabbed)
}

sp {top-state*world*object*predicates*elaborate*is-visible*internal
   (state <s> ^superstate nil
              ^agent-params.domain internal
             -^agent-params.simulate-perception true
              ^world.objects.object.predicates <preds>)
-->
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1)
}

