# If the object is missing and not a task object - delete it
sp {anystate*apply*attend-to-scene-change*missing-object*delete*object
   (state <s> ^operator <o>)
   (<o> ^name attend-to-scene-change
        ^change <change>)
   (<change> ^type missing-object
             ^object-info <obj-info>)
   (<obj-info> ^wm-obj <obj>)
   (<obj> -^task-object true)
-->
   (<obj-info> ^delete-object true)
}

# If the object is a task object, only delete the belief rep
sp {anystate*apply*attend-to-scene-change*missing-object*delete*belief*object
   (state <s> ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name attend-to-scene-change
        ^change <change>)
   (<change> ^type missing-object
             ^object-info <obj-info>)
   (<obj-info> ^wm-obj <obj>
               ^belief-obj.id <id>)
   (<obj> ^task-object true)
-->
   (<cmd> ^delete_node.id <id>)
}

# if print-perception is on, print a message
sp {anystate*apply*attend-to-scene-change*attend-to-missing-object*print-perception*print
   (state <s> ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name attend-to-scene-change
        ^change <c>)
   (<c> ^type missing-object
        ^object-info.object-handle <handle>)
-->
   (write |Missing object | <handle> (crlf))
}
