# An imagined object matches an existing world object
# We add a link to the world object, we don't create a new copy

sp {update-existing-object*propose*link-imagined-object
  (state <s> ^name update-existing-object
             ^source-type imagined-obj
             ^source-obj <imag>
             ^existing-obj.wm-obj { <obj> <> <imag> })
  (<imag> -^linked-to <obj>)
-->
   (<s> ^operator <o> +)
   (<o> ^name link-imagined-object
        ^imagined-obj <imag>
        ^existing-obj <obj>)
}

sp {update-existing-object*apply*link-imagined-object
   (state <s> ^name update-existing-object
              ^operator <o>)
   (<o> ^name link-imagined-object
        ^imagined-obj <imag>
        ^existing-obj <obj>)
-->
  (<imag> ^linked-to <obj>)
}

# If print-perception true, report that the report that the new-object was ignored (possibly an error)
sp {update-existing-object*apply*link-imagined-object*print-perception*print
   (state <s> ^name update-existing-object
              ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name link-imagined-object
        ^imagined-obj.handle <imag-handle>
        ^existing-obj.handle <obj-handle>)
-->
   (write |Linking imagined object | <imag-handle> | to existing object | <obj-handle> (crlf))
}
