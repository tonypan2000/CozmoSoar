# Elaborate the source object information from the superoperator
sp {match-existing-object*elaborate*source*info*from*superoperator
   (state <s> ^name match-existing-object
              ^superstate.operator <ssop>)
   (<ssop> ^source-type <type>
           ^source-obj <obj>)
-->
   (<s> ^source-type <type>
        ^source-obj <obj>)
}

sp {match-existing-object*elaborate*match-missing-object*superstate
   (state <s> ^name match-existing-object
              ^superstate.candidate-for-missing-object <obj>)
-->
   (<s> ^match-missing-object <obj>)
}

# Elaborate the object-monitor wme from the top-state
sp {match-existing-object*elaborate*object-monitor*from*top-state
   (state <s> ^name match-existing-object
              ^top-state.perception-monitor.object-monitor <omon>)
-->
   (<s> ^object-monitor <omon>)
}
              
# If it is a perception object, elaborate the new-object-info that contains it
sp {match-existing-object*elaborate*object-info
   (state <s> ^name match-existing-object
              ^source-type perception-obj
              ^source-obj <obj>
              ^object-monitor.new-object-info <new-obj-info>)
   (<new-obj-info> ^perception-obj <obj>)
-->
   (<s> ^object-info <new-obj-info>)
} 

# Elaborate the contained-threshold parameter used to determine if an object is contained in another
sp {match-existing-object*elaborate*contained-threshold
   (state <s> ^name match-existing-object
              ^agent-params.contained-threshold <thresh>)
-->
   (<s> ^contained-threshold <thresh>)
}

# WME used to hold results from overlap tests
sp {match-existing-object*elaborate*overlap-results
   (state <s> ^name match-existing-object)
-->
   (<s> ^overlap-results <cmd>)
}

# Elaborates information from the object-monitor for an overlapping object
sp {match-existing-object*elaborate*overlap-result*overlapper-info
   (state <s> ^name match-existing-object
              ^overlap-results.result <res>
              ^object-monitor.object-info <known-obj-info>)
   (<res> ^object-b <known-bel-id>)
   (<known-obj-info> ^belief-obj.id <known-bel-id>)
-->
   (<res> ^overlapper-info <known-obj-info>)
}
   
