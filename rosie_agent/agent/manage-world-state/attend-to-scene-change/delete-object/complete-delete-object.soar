sp {delete-object*propose*complete-delete-object
   (state <s> ^name delete-object)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-delete-object)
}

# Removes the object-info from the object-monitor
sp {delete-object*apply*complete-delete-object*remove*object-info
   (state <s> ^name delete-object
              ^operator.name complete-delete-object
              ^object-info <obj-info>
              ^top-state.perception-monitor.object-monitor <obj-mon>)
   (<obj-mon> ^object-info <obj-info>)
-->
   (<obj-mon> ^object-info <obj-info> -)
}

# Removes the object from top-state.world
sp {delete-object*apply*complete-delete-object*remove*from*world
   (state <s> ^name delete-object
              ^operator.name complete-delete-object
              ^object-info.wm-obj <obj>
              ^top-state.world.objects <objs>)
   (<objs> ^object <obj>)
-->
   (<objs> ^object <obj> -)
}

# Removes the belief-obj from svs
sp {delete-object*apply*operator*complete-delete-object*remove*from*svs
   (state <s> ^name delete-object
              ^operator.name complete-delete-object
              ^object-info.belief-obj.id <id>
              ^top-state.svs.command <cmd>)
-->
   (<cmd> ^delete_node.id <id>)
}

# Removes any predicate instances involving the object from the world
sp {delete-object*apply*operator*complete-delete-object*remove*predicates
   (state <s> ^name delete-object
              ^operator.name complete-delete-object
              ^object-info.wm-obj <obj>
              ^top-state.world.predicates.predicate <pred>)
   (<pred> ^instance <i>)
   (<i> ^<num> <obj>)
-->
   (<pred> ^instance <i> -)
}

# Explicitly removes the change from the perception-monitor
sp {delete-object*apply*complete-delete-object*remove*change
   (state <s> ^name delete-object
              ^operator.name complete-delete-object
              ^superstate.scene-change <c>
              ^top-state.perception-monitor.changes <chgs>)
   (<chgs> ^change <c>)
-->
   (<chgs> ^change <c> -)
}

# if print-perception is on, print a message
sp {delete-object*apply*complete-delete-object*print-perception*print
   (state <s> ^name delete-object
              ^operator.name complete-delete-object
              ^object-info.object-handle <handle>
              ^agent-params.print-perception true)
-->
   (write |Deleting object| <handle> (crlf))
}
