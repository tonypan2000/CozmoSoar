# Add a confirmed false flag, and sets a time to check if the object is still stable
sp {anystate*apply*attend-to-scene-change*new-perception-object*add*status
   (state <s> ^operator <o>
              ^io.input-link.time.seconds <t>
              ^agent-params.stability-check-duration <dur>)
   (<o> ^name attend-to-scene-change
        ^change <change>)
   (<change> ^type new-perception-object
             ^object-info <obj-info>)
-->
   (<obj-info> ^confirmed false
               ^check-stability-time (+ <t> <dur>)
               ^number-checks 0)
}

# Creates filters to monitor if the object changes position/size
sp {anystate*apply*attend-to-scene-change*new-perception-object*create*filters
   (state <s> ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name attend-to-scene-change
        ^change <change>)
   (<change> ^type new-perception-object
             ^object-info <obj-info>)
   (<obj-info> ^perception-obj.id <id>)
-->
   (<cmd> ^extract <pos>)
   (<pos> ^type monitor_position
          ^a <pa>)
   (<pa> ^type node
         ^id <id>)
   (<obj-info> ^position-monitor <pos>)
   
   (<cmd> ^extract <vol>)
   (<vol> ^type monitor_volume
          ^a <va>)
   (<va> ^type node
         ^id <id>)
   (<obj-info> ^volume-monitor <vol>)
}
