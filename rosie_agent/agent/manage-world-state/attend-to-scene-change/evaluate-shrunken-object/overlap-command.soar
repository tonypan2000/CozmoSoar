# Find all new perceptual objects (aren't in belief yet) that are contained by the belief object

sp {evaluate-shrunken-object*propose*overlap-command*contains
   (state <s> ^name evaluate-shrunken-object
              ^object-info.belief-obj.id <stale-id>
              ^intersect-command.result {<perc-id> <> none}
             -^top-state.perception-monitor.object-monitor.object-info.perception-obj.id <perc-id>
             -^overlap-results.result.object-a <perc-id>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name overlap-command
        ^object-a <perc-id>
        ^object-b <stale-id>)
}

sp {evaluate-shrunken-object*apply*overlap-command*copy-result
   (state <s> ^name evaluate-shrunken-object
              ^operator.name overlap-command
              ^overlap-command <cmd>
              ^overlap-results <results>
              ^agent-params.print-perception <flag>)
   (<cmd> ^result <res>)
   (<res> ^overlap <> none)
-->
   (<s> ^overlap-command <cmd> -)
   (<results> ^result <res>)
}

sp {evaluate-shrunken-object*apply*overlap-command*print-perception*report*overlap
   (state <s> ^name evaluate-shrunken-object
              ^operator.name overlap-command
              ^overlap-command.result <res>
              ^agent-params.print-perception true)
   (<res> ^object-a <a> ^object-b <b> ^overlap <val>)
-->
   (write |Overlap: | <val> | of | <a> | is contained by | <b> (crlf))
} 
