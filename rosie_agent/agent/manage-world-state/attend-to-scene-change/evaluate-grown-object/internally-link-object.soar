sp {evaluate-grown-object*propose*internally-link-object
   (state <s> ^name evaluate-grown-object
              ^found-contained-object true
              ^object-info <obj-info>)
-->
   (<s> ^operator <o> +)
   (<o> ^name internally-link-objects
        ^src-info <obj-info>)
}

sp {evaluate-grown-object*elaborate*internally-link-objects*dest-id
   (state <s> ^name evaluate-grown-object
              ^operator <o> +
              ^contained-object-info <dest-info>)
   (<o> ^name internally-link-objects)
-->
   (<o> ^dest-info <dest-info>)
}

sp {evaluate-grown-object*apply*internally-link-objects*link
   (state <s> ^name evaluate-grown-object
              ^operator <o>)
   (<o> ^name internally-link-objects
        ^src-info.object-handle <src-handle>
        ^dest-info <dest-info>)
-->
   (<dest-info> ^internal-link <src-handle>)
}

sp {evaluate-grown-object*apply*internally-link-objects*monitor*perception
   (state <s> ^name evaluate-grown-object
              ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name internally-link-objects
        ^dest-info.object-handle <dest-handle>
        ^src-info.object-handle <src-handle>)
-->
   (write |Internally linking object | <dest-handle> | to | <src-handle> (crlf))
}
        
