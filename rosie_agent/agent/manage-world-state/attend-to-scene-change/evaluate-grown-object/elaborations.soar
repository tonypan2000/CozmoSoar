sp {evaluate-grown-object*elaborate*object-info
   (state <s> ^name evaluate-grown-object
              ^superstate.operator.object-info <obj-info>)
-->
   (<s> ^object-info <obj-info>)
}

sp {evaluate-grown-object*elaborate*agent-params*contained-threshold
   (state <s> ^name evaluate-grown-object
              ^agent-params.contained-threshold <t>)
-->
   (<s> ^contained-threshold <t>)
}

sp {evaluate-grown-object*elaborate*overlap-results
   (state <s> ^name evaluate-grown-object)
-->
   (<s> ^overlap-results <res>)
}

sp {evaluate-grown-object*elaborate*contained-object
   (state <s> ^name evaluate-grown-object
              ^object-info.perception-obj.id <grown-id>
              ^overlap-results.result <res>
              ^contained-threshold <t>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<res> ^object-a <a>
          ^object-b <grown-id>
          ^overlap > <t>)
   (<obj-info> ^belief-obj.id <a>)
-->
   (<s> ^contained-object-info <obj-info>)
}

sp {evaluate-grown-object*elaborate*found-contained-object
   (state <s> ^name evaluate-grown-object
              ^contained-object-info <obj>)
-->
   (<s> ^found-contained-object true)
}
