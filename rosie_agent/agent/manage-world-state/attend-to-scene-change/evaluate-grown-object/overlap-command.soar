sp {evaluate-grown-object*propose*overlap-command*contains
   (state <s> ^name evaluate-grown-object
              ^object-info.perception-obj.id <perc-id>
              ^intersect-command.result {<belief-id> <> none}
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
             -^overlap-results.result.object-a <belief-id>)
   (<obj-info> ^belief-obj.id <belief-id>
              -^perception-obj)
-->
   (<s> ^operator <o> + =)
   (<o> ^name overlap-command
        ^object-a <belief-id>
        ^object-b <perc-id>)
}

sp {evaluate-grown-object*apply*overlap-command*copy-result
   (state <s> ^name evaluate-grown-object
              ^operator.name overlap-command
              ^overlap-command <cmd>
              ^overlap-results <results>)
   (<cmd> ^result <res>)
   (<res> ^overlap <> none)
-->
   (<s> ^overlap-command <cmd> -)
   (<results> ^result <res>)
}

sp {evaluate-grown-object*apply*overlap-command*print-perception*report*overlap
   (state <s> ^name evaluate-grown-object
              ^operator.name overlap-command
              ^overlap-command.result <res>
              ^agent-params.print-perception true)
   (<res> ^object-a <a> ^object-b <b> ^overlap <val>)
-->
   (write |Overlap: | <val> | of | <a> | is contained by | <b> (crlf))
} 
