sp {evaluate-grown-object*propose*copy-object
   (state <s> ^name evaluate-grown-object
              ^object-info <obj-info>)
-->
   (<s> ^operator <o> +)
   (<o> ^name copy-object
        ^object-info <obj-info>)
}

sp {evaluate-grown-object*apply*copy-object
   (state <s> ^name evaluate-grown-object
              ^operator <o>)
   (<o> ^name copy-object
        ^object-info <obj-info>)
-->
   (<obj-info> ^update-pose true)
}

sp {evaluate-grown-object*apply*copy-object*mark-recheck-growth
   (state <s> ^name evaluate-grown-object
              ^operator <o>
              ^agent-params.recheck-growth-delay <d>
              ^io.input-link.time.seconds <t>)
   (<o> ^name copy-object
        ^object-info.differences <diffs>)
-->
   (<diffs> ^recheck-growth-at (+ <t> <d>))
}

sp {evaluate-grown-object*apply*copy-object*remove*old*recheck-growth
   (state <s> ^name evaluate-grown-object
              ^operator <o>
              ^io.input-link.time.seconds <t>)
   (<o> ^name copy-object
        ^object-info.differences <diffs>)
   (<diffs> ^recheck-growth-at {<r> < <t>})
-->
   (<diffs> ^recheck-growth-at <r> -)
}
