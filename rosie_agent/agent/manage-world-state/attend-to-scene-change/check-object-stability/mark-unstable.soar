# If the object is not stable, then increment the timers and reset the filters
sp {check-object-stability*propose*mark-unstable
   (state <s> ^name check-object-stability
              ^obj-is-stable false
              ^object-info <obj-info>)
-->
   (<s> ^operator <o> +)
   (<o> ^name mark-unstable
        ^object-info <obj-info>)
}

# Increment the timers to check again later
sp {check-object-stability*apply*mark-unstable*reset*timers
   (state <s> ^name check-object-stability
              ^operator <o>
              ^io.input-link.time.seconds <cur-t>
              ^agent-params.stability-check-duration <dur>)
   (<o> ^name mark-unstable
        ^object-info <obj-info>)
   (<obj-info> ^check-stability-time <old-t>
               ^number-checks <num>)
-->
   (<obj-info> ^check-stability-time <old-t> -
               ^check-stability-time (+ <cur-t> <dur>)
               ^number-checks <num> -
               ^number-checks (+ <num> 1))
}

# Remove the old filters
sp {check-object-stability*apply*mark-unstable*remove*filters
   (state <s> ^name check-object-stability
              ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name mark-unstable
         ^object-info <obj-info>)
   (<obj-info> ^perception-obj.id <id>
               ^position-monitor <old-pos>
               ^volume-monitor <old-vol>)
-->
   (<cmd> ^extract <old-pos> -
          ^extract <old-vol> -)
   (<obj-info> ^position-monitor <old-pos> -
               ^volume-monitor <old-vol> -)   
}

# Create new filters
sp {check-object-stability*apply*mark-unstable*reset*filters
   (state <s> ^name check-object-stability
              ^operator <o>
              ^top-state.svs.command <cmd>)
   (<o> ^name mark-unstable
        ^object-info <obj-info>)
   (<obj-info> ^perception-obj.id <id>)
-->
   (<cmd> ^extract <pos>)
   (<pos> ^type monitor_position
          ^a <pa>)
   (<pa> ^type node
         ^id <id>)
   (<obj-info> ^position-monitor <pos>)
   
   (<cmd> ^extract <vol>)
   (<vol> ^type monitor_volume
          ^a <va>)
   (<va> ^type node
         ^id <id>)
   (<obj-info> ^volume-monitor <vol>)
}
