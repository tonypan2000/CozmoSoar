sp {check-object-stability*elaborate*object-info
   (state <s> ^name check-object-stability
              ^superstate.operator.object-info <obj-info>)
-->
   (<s> ^object-info <obj-info>)
}

#####################
# Elaborations that determine whether an object is stable or not

sp {check-object-stability*elaborate*thresholds
   (state <s> ^name check-object-stability
              ^agent-params <params>)
   (<params> ^pos-diff-threshold <pdt>
             ^vol-low-diff-threshold <vlt>
             ^vol-high-diff-threshold <vht>
             ^max-stability-checks <max>)
-->
   (<s> ^pos-threshold <pdt>
        ^vol-low-threshold <vlt>
        ^vol-high-threshold <vht>
        ^max-checks <max>)
}

sp {check-object-stability*elaborate*pos-diff
   (state <s> ^name check-object-stability
              ^object-info.position-monitor.result.record.value <val>)
-->
   (<s> ^pos-diff <val>)
}

sp {check-object-stability*elaborate*vol-diff
   (state <s> ^name check-object-stability
              ^object-info.volume-monitor.result.record.value <val>)
-->
   (<s> ^vol-diff <val>)
}

# stable position

sp {check-object-stability*elaborate*stable-pos*true
   (state <s> ^name check-object-stability
              ^pos-threshold <t>
              ^pos-diff < <t>)
-->
   (<s> ^stable-pos true)
}

sp {check-object-stability*elaborate*stable-pos*false
   (state <s> ^name check-object-stability
              ^pos-threshold <t>
              ^pos-diff >= <t>)
-->
   (<s> ^stable-pos false)
}

# stable volume

sp {check-object-stability*elaborate*stable-vol*true
   (state <s> ^name check-object-stability
              ^vol-low-threshold <low>
              ^vol-high-threshold <high>
              ^vol-diff { < <high> > <low>})
-->
   (<s> ^stable-vol true)
}

sp {check-object-stability*elaborate*stable-vol*false*low
   (state <s> ^name check-object-stability
              ^vol-low-threshold <low>
              ^vol-diff <= <low>)
-->
   (<s> ^stable-vol false)
}

sp {check-object-stability*elaborate*stable-vol*false*high
   (state <s> ^name check-object-stability
              ^vol-high-threshold <high>
              ^vol-diff >= <high>)
-->
   (<s> ^stable-vol false)
}

# Overall stability

# Always stable in internal world
sp {check-object-stability*elaborate*obj-is-stable*true*internal
   (state <s> ^name check-object-stability
              ^agent-params.domain internal)
-->
   (<s> ^obj-is-stable true)
}
sp {check-object-stability*elaborate*obj-is-stable*true
   (state <s> ^name check-object-stability
              ^stable-vol true
              ^stable-pos true)
-->
   (<s> ^obj-is-stable true)
}

sp {check-object-stability*elaborate*obj-is-stable*true*timeout
   (state <s> ^name check-object-stability
              ^{ << stable-pos stable-vol >> } false
              ^max-checks <max>
              ^object-info.number-checks >= <max>)
-->
   (<s> ^obj-is-stable true)
}

sp {check-object-stability*elaborate*obj-is-stable*false*more-checks
   (state <s> ^name check-object-stability
              ^{ << stable-pos stable-vol >> } false
              ^max-checks <max>
              ^object-info.number-checks < <max>)
-->
   (<s> ^obj-is-stable false)
}
 
