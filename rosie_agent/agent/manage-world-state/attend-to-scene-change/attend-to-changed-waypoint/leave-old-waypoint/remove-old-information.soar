# Removes information about the previous location from working memory
#   (Causes the substate to be removed)
sp {leave-old-waypoint*propose*remove-old-information
   (state <s> ^name leave-old-waypoint)
-->
   (<s> ^operator <o> +)
   (<o> ^name remove-old-information)
}

# Delete all objects from working memory
sp {leave-old-waypoint*apply*remove-old-information*attend*delete-object
   (state <s> ^name leave-old-waypoint
              ^operator.name remove-old-information
              ^top-state.perception-monitor.changes <chgs>
              ^confirmed-object <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^wm-obj <obj>)
   (<obj> -^task-object true
          -^predicates.arm-status grabbed)
-->
   (<chgs> ^change <c>)
   (<c> ^type delete-object
        ^object-info <obj-info>)
}

sp {leave-old-waypoint*apply*remove-old-information*remove*belief*task*objects
   (state <s> ^name leave-old-waypoint
              ^operator.name remove-old-information
              ^confirmed-object <obj>
              ^top-state.svs.command <cmd>)
   (<obj> ^task-object true
          ^svs-obj.id <id>)
-->
   (<cmd> ^delete_node.id <id>)
}

# Remove current-waypoint from robot
sp {leave-old-waypoint*apply*remove-old-information*remove*old-waypoint
   (state <s> ^name leave-old-waypoint
              ^operator.name remove-old-information
              ^old-waypoint <wp>
              ^top-state.world.robot <rob>)
   (<rob> ^current-waypoint <wp>)
-->
   (<rob> ^current-waypoint <wp> -)
}

# Remove current-location from robot
sp {leave-old-waypoint*apply*remove-old-information*remove*old-location
   (state <s> ^name leave-old-waypoint
              ^operator.name remove-old-information
              ^old-location <loc>
              ^top-state.world.robot <rob>)
   (<rob> ^current-location <loc>)
-->
   (<rob> ^current-location <loc> -)
}

# Remove svs filters from command
sp {leave-old-waypoint*apply*remove-old-information*cleanup*svs*filters
   (state <s> ^name leave-old-waypoint
              ^operator.name remove-old-information
              ^{ <type> << position-filter rotation-filter scale-filter >> } <filter>
              ^top-state.svs.command <cmd>)
   (<cmd> ^extract_once <filter>)
-->
   (<cmd> ^extract_once <filter> -)
}

# SCANOP
#sp {leave-old-waypoint*apply*remove-old-information*remove*scanned*flags
#   (state <s> ^name leave-old-waypoint
#              ^operator.name remove-old-information
#              ^top-state.world.robot <rob>)
#   (<rob> ^{ <att> << havent-scanned scanned >> } <loc> )
#-->
#   (<rob> ^<att> <loc> -)
#}
   
## Change the rest of the objects
#sp {leave-old-waypoint*apply*remove-old-information*set*not-visible
#   (state <s> ^name leave-old-waypoint
#              ^operator.name remove-old-information
#              ^top-state.world.objects.object <obj>)
#   (<obj> ^predicates <preds>)
#   (<preds> ^is-visible1 visible1)
#-->
#   (<preds> ^is-visible1 visible1 -
#            ^is-visible1 not-visible1)
#}
#
#sp {leave-old-waypoint*apply*remove-old-information*set*not-confirmed
#   (state <s> ^name leave-old-waypoint
#              ^operator.name remove-old-information
#              ^top-state.world.objects.object <obj>)
#   (<obj> ^predicates <preds>)
#   (<preds> ^is-confirmed1 confirmed1)
#-->
#   (<preds> ^is-confirmed1 confirmed1 -
#            ^is-confirmed1 not-confirmed1)
#}

sp {leave-old-waypoint*remove*any*metric*info
   (state <s> ^name leave-old-waypoint
              ^operator.name remove-old-information
              ^top-state.world.objects.object <obj>)
   (<obj> ^{ <att> << position rotation scale >> } <val>)
-->
   (<obj> ^<att> <val> -)
}
