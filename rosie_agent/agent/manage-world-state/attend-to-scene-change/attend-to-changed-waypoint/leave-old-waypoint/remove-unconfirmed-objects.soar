# Delete each unconfirmed object (don't want part of the episodic memory state)
sp {leave-old-waypoint*propose*remove-unconfirmed-objects
   (state <s> ^name leave-old-waypoint
              ^unconfirmed-objects true)
-->
   (<s> ^operator <o> +)
   (<o> ^name remove-unconfirmed-objects)
}

sp {leave-old-waypoint*elaborate*remove-unconfirmed-objects*remove-object
   (state <s> ^name leave-old-waypoint
              ^operator <o> +
              ^unconfirmed-object <obj>)
   (<o> ^name remove-unconfirmed-objects)
-->
   (<o> ^remove-object <obj>)
}

sp {leave-old-waypoint*apply*remove-unconfirmed-objects*attend*delete-object
   (state <s> ^name leave-old-waypoint
              ^operator <o>
              ^top-state.perception-monitor.changes <chgs>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name remove-unconfirmed-objects
        ^remove-object <obj>)
   (<obj-info> ^wm-obj <obj>)
-->
   (<chgs> ^change <c>)
   (<c> ^type delete-object
        ^object-info <obj-info>)
}