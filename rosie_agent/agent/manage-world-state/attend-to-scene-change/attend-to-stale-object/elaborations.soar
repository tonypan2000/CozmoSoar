# Superoperator Elaborations
sp {attend-to-stale-object*elaborate*object-info
   (state <s> ^name attend-to-stale-object
              ^superstate.operator.object-info <obj-info>)
-->
   (<s> ^object-info <obj-info>)
}

sp {attend-to-stale-object*elaborate*missing*true
   (state <s> ^name attend-to-stale-object
              ^object-info.missing true)
-->
   (<s> ^missing true)
}

#sp {attend-to-stale-object*elaboate*aggressive-match 
#   (state <s> ^name attend-to-stale-object
#              ^superstate.superstate.operator.aggressive-match <m>)
#-->
#   (<s> ^aggressive-match <m>)
#}

# Parameter Elaborations
sp {attend-to-stale-object*elaborate*agent-params*occlusion-check-period
   (state <s> ^name attend-to-stale-object
              ^agent-params.occlusion-check-period <p>)
-->
   (<s> ^occlusion-check-period <p>)
}

sp {attend-to-stale-object*elaborate*agent-params*contained-threshold
   (state <s> ^name attend-to-stale-object
              ^agent-params.contained-threshold <t>)
-->
   (<s> ^contained-threshold <t>)
}

sp {attend-to-stale-object*elaborate*agent-params*contained-by-threshold
   (state <s> ^name attend-to-stale-object
              ^agent-params.contained-by-threshold <t>)
-->
   (<s> ^contained-by-threshold <t>)
}

# Intersection Results
sp {attend-to-stale-object*elaborate*intersected-perc-id*from*intersect-command
   (state <s> ^name attend-to-stale-object
              ^intersect-command.result { <obj-id> <> none })
-->
   (<s> ^intersected-perc-id <obj-id>)
}

# Overlap Results
sp {attend-to-stale-object*elaborate*overlap-results
   (state <s> ^name attend-to-stale-object)
-->
   (<s> ^overlap-results <res>)
}

sp {attend-to-stale-object*elaborate*contained-fragment
   (state <s> ^name attend-to-stale-object
              ^object-info.belief-obj.id <stale-id>
              ^overlap-results.result <res>
              ^contained-threshold <t>)
   (<res> ^object-a <cont-id>
          ^object-b <stale-id>
          ^overlap > <t>)
-->
   (<s> ^contained-fragment-id <cont-id>)
}

sp {attend-to-stale-object*elaborate*contained-by
   (state <s> ^name attend-to-stale-object
              ^object-info.belief-obj.id <stale-id>
              ^overlap-results.result <res>
              ^contained-by-threshold <t>
              ^top-state.perception-monitor.object-monitor.object-info <cont-obj-info>)
   (<res> ^object-a <stale-id>
          ^object-b <cont-id>
          ^overlap > <t>)
   (<cont-obj-info> ^perception-obj.id <cont-id>)
-->
   (<s> ^contained-by <cont-obj-info>)
}

sp {attend-to-stale-object*elaborate*found-contained-fragment
   (state <s> ^name attend-to-stale-object
              ^contained-fragment-id <id>)
-->
   (<s> ^found-contained-fragment true)
}
