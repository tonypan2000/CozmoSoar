sp {attend-to-stale-object*propose*internally-link-objects
   (state <s> ^name attend-to-stale-object
              ^contained-by <src-obj-info>
              ^object-info <dest-obj-info>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name internally-link-objects
        ^src-info <src-obj-info>
        ^dest-info <dest-obj-info>)
}

sp {attend-to-stale-object*elaborate*internally-link-objects*overlap
   (state <s> ^name attend-to-stale-object
              ^operator <o> +
              ^overlap-results.result <res>)
   (<o> ^name internally-link-objects
        ^src-info.perception-obj.id <src-perc-id>)
   (<res> ^object-b <src-perc-id>
          ^overlap <overlap>)
-->
   (<o> ^overlap <overlap>)
}

sp {attend-to-stale-object*prefer*internally-link-objects*higher*overlap
   (state <s> ^name attend-to-stale-object
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name internally-link-objects
         ^overlap <overlap1>)
   (<o2> ^name internally-link-objects
         ^overlap < <overlap1>)
-->
   (<s> ^operator <o1> > <o2>)
}
   
sp {attend-to-stale-object*apply*internally-link-objects
   (state <s> ^name attend-to-stale-object
              ^operator <o>)
   (<o> ^name internally-link-objects
        ^src-info.object-handle <src-handle>
        ^dest-info <dest-obj-info>)
-->
   (<dest-obj-info> ^internal-link <src-handle>)
}

sp {attend-to-stale-object*apply*internally-link-objects*monitor*perception
   (state <s> ^name attend-to-stale-object
              ^operator <o>
              ^agent-params.print-perception true)
   (<o> ^name internally-link-objects
        ^src-info.object-handle <src>
        ^dest-info.object-handle <dest>)
-->
   (write |Internally linking object | <dest> | to | <src> (crlf))
}
        
