# operator construct-world-object
# If the object doesn't match an existing one, create a new representation for it
sp {insert-object-into-world*propose*construct-world-object
   (state <s> ^name insert-object-into-world
              ^source-obj <source>
              ^source-type <type>
              ^matches-existing-object none
             -^new-object-rep)
-->
   (<s> ^operator <o> +)
   (<o> ^name construct-world-object
        ^source-obj <source>
        ^source-type <type>
        ^destination <s>
        ^result-name new-object-rep)
}

# elaborate smem-obj if one was retrieved
sp {insert-object-into-world*elaborate*construct-world-object*smem-obj*non*perception-obj
   (state <s> ^name insert-object-into-world
              ^source-type <> perception-obj
              ^operator <o> +
              ^matches-smem-object { <lti> <> none })
   (<o> ^name construct-world-object)
-->
   (<o> ^smem-obj <lti>)
}

# for a perception-obj, only add the smem info if it doesn't match an object already in the world 
sp {insert-object-into-world*elaborate*construct-world-object*smem-obj*perception-obj
   (state <s> ^name insert-object-into-world
              ^source-type perception-obj
              ^operator <o> +
              ^matches-smem-object { <lti> <> none }
              ^object-monitor <obj-mon>)
   (<o> ^name construct-world-object)
   (<lti> ^handle <handle>)
   (<obj-mon> -^object-info.object-handle <handle>)
-->
   (<o> ^smem-obj <lti>)
}
