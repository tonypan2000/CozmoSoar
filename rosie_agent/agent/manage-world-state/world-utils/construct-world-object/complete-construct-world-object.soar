# Once the object is created, copy it to the destination
sp {construct-world-object*propose*complete-construct-world-object
   (state <s> ^name construct-world-object
              ^new-object-rep <rep>
              ^copied-predicates true)
-->
   (<s> ^operator <o> +)
   (<o> ^name complete-construct-world-object
        ^new-object-rep <rep>)
}

sp {construct-world-object*apply*complete-construct-world-object
   (state <s> ^name construct-world-object
              ^operator <o>
              ^destination <dest>
              ^result-name <res-name>)
   (<o> ^name complete-construct-world-object
        ^new-object-rep <obj>)
-->
   (<dest> ^<res-name> <obj>)
}

sp {construct-world-object*apply*complete-construct-world-object*add*linked-to*imagined
   (state <s> ^name construct-world-object
              ^operator <o>
              ^source-type imagined-obj
              ^source-obj <src-obj>)
   (<o> ^name complete-construct-world-object
        ^new-object-rep <obj>)
-->
   (<src-obj> ^linked-to <obj>)
}
