# Copy predicates onto the object
 
### PROPOSAL
sp {construct-world-object*propose*copy-predicates
   (state <s> ^name construct-world-object
              ^new-object-rep.predicates <preds>
             -^copied-predicates)
-->
   (<s> ^operator <o> +)
   (<o> ^name copy-predicates)
}

### ELABORATION
# Elaborate predicates to copy from imagined-obj
sp {construct-world-object*elaborate*copy-predicates*imagined-predicate
   (state <s> ^name construct-world-object
              ^operator <o> +
              ^source-type imagined-obj
              ^source-obj.predicates.<prop> <pred>
              ^new-object-rep.predicates <preds>
             -^ignore-predicate <prop>)
   (<o> ^name copy-predicates)
-->
   (<o> ^imagined-predicate <p>)
   (<p> ^property <prop>
        ^predicate <pred>)
}

# Elaborate predicates to copy from default-arg
sp {construct-world-object*elaborate*copy-predicates*imagined-predicate*default-arg
   (state <s> ^name construct-world-object
              ^operator <o> +
              ^source-type default-arg
              ^source-obj.default.<prop> <pred>
              ^new-object-rep.predicates <preds>
             -^ignore-predicate <prop>)
   (<o> ^name copy-predicates)
-->
   (<o> ^imagined-predicate <p>)
   (<p> ^property <prop>
        ^predicate <pred>)
}

# Elaborate predicates to copy from perception
sp {construct-world-object*elaborate*copy-predicates*perception-predicate
   (state <s> ^name construct-world-object
              ^operator <o> +
              ^source-type perception-obj
              ^object-info <obj-info>
              ^new-object-rep.predicates <preds>
             -^ignore-predicate <prop>)
   (<o> ^name copy-predicates)
   (<obj-info> ^properties.property-info <prop-info>)
   (<prop-info> ^property-handle <prop>
                ^predicate-handle <pred>)
-->
   (<o> ^perception-predicate <p>)
   (<p> ^property <prop>
        ^predicate <pred>)
}

# Elaborate predicates to copy from smem
sp {construct-world-object*elaborate*copy-predicates*smem-predicate
   (state <s> ^name construct-world-object
              ^operator <o> +
              ^smem-obj.<prop> <pred>
              ^new-object-rep.predicates <preds>
             -^ignore-predicate <prop>)
   (<o> ^name copy-predicates)
-->
   (<o> ^smem-predicate <p>)
   (<p> ^property <prop>
        ^predicate <pred>)
}

# Elaborate predicates to copy from epmem
sp {construct-world-object*elaborate*copy-predicates*epmem-predicate
   (state <s> ^name construct-world-object
              ^operator <o> +
              ^epmem-obj.predicates.<prop> <pred>
              ^new-object-rep.predicates <preds>
             -^ignore-predicate <prop>)
   (<o> ^name copy-predicates)
-->
   (<o> ^epmem-predicate <p>)
   (<p> ^property <prop>
        ^predicate <pred>)
}

### APPLICATION
# Add a copied-predicates flag to the state
sp {construct-world-object*apply*copy-predicates*mark*copied-predicates
   (state <s> ^name construct-world-object
              ^operator.name copy-predicates)
-->
   (<s> ^copied-predicates true)
}

# Priority 1: Predicates from perception or imagined
sp {construct-world-object*apply*copy-predicate*copy*perception-predicate
   (state <s> ^name construct-world-object
              ^operator <o>
              ^new-object-rep.predicates <preds>)
   (<o> ^name copy-predicates
        ^perception-predicate <p>)
   (<p> ^property <prop>
        ^predicate <pred>)
-->
   (<preds> ^<prop> <pred>)
}

sp {construct-world-object*apply*copy-predicate*copy*imagined-predicate
   (state <s> ^name construct-world-object
              ^operator <o>
              ^new-object-rep.predicates <preds>)
   (<o> ^name copy-predicates
        ^imagined-predicate <p>)
   (<p> ^property <prop>
        ^predicate <pred>)
-->
   (<preds> ^<prop> <pred>)
}

# Priority 2: Predicates from smem
sp {construct-world-object*apply*copy-predicate*copy*smem-predicate
   (state <s> ^name construct-world-object
              ^operator <o>
              ^new-object-rep.predicates <preds>)
   (<o> ^name copy-predicates
        ^smem-predicate <p>
       -^perception-predicate.property <prop>)
   (<p> ^property <prop>
        ^predicate <pred>)
-->
   (<preds> ^<prop> <pred>)
}

sp {construct-world-object*apply*copy-predicate*smem*copy*waypoint
   (state <s> ^name construct-world-object
              ^operator.name copy-predicates
              ^smem-obj.waypoint <wp>
              ^new-object-rep <rep>)
-->
   (<rep> ^waypoint <wp>)
}

# Priority 3: Predicates from epmem
sp {construct-world-object*apply*copy-predicate*copy*epmem-predicate
   (state <s> ^name construct-world-object
              ^operator <o>
              ^new-object-rep.predicates <preds>)
   (<o> ^name copy-predicates
        ^epmem-predicate <p>
       -^perception-predicate.property <prop>
       -^smem-predicate.property <prop>)
   (<p> ^property <prop>
        ^predicate <pred>)
-->
   (<preds> ^<prop> <pred>)
}
