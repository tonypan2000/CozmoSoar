# Elaborate the source object information from the superoperator
sp {construct-world-object*elaborate*source*info*from*superoperator
   (state <s> ^name construct-world-object
              ^superstate.operator <ssop>)
   (<ssop> ^source-type <type>
           ^source-obj <obj>
           ^destination <dest>
           ^result-name <res-name>)
-->
   (<s> ^source-type <type>
        ^source-obj <obj>
        ^destination <dest>
        ^result-name <res-name>)
}

# Elaborate smem-obj from superoperator
sp {construct-world-object*elaborate*smem-obj*from*superoperator
   (state <s> ^name construct-world-object
              ^superstate.operator <ssop>)
   (<ssop> ^smem-obj <obj>)
-->
   (<s> ^smem-obj <obj>)
}

sp {construct-world-object*elaborate*smem-obj*source-object
   (state <s> ^name construct-world-object
              ^source-type smem-obj
              ^source-obj <obj>)
-->
   (<s> ^smem-obj <obj>)
}

# Elaborate epmem-obj from source
sp {construct-world-object*elaborate*epmem-obj*source-object
   (state <s> ^name construct-world-object
              ^source-type epmem-obj
              ^source-obj <obj>)
-->
   (<s> ^epmem-obj <obj>)
}

# Elaborate object-monitor from top-state
sp {construct-world-object*elaborate*object-monitor
   (state <s> ^name construct-world-object
              ^top-state.perception-monitor.object-monitor <obj-mon>)
-->
   (<s> ^object-monitor <obj-mon>)
}

# Elaborate object-info from object-monitor
sp {construct-world-object*elaborate*input-link-obj*matching*perception-obj
   (state <s> ^name construct-world-object
              ^source-type perception-obj
              ^source-obj <pobj>
              ^object-monitor.new-object-info <obj-info>)
   (<obj-info> ^perception-obj <pobj>)
-->
   (<s> ^object-info <obj-info>)
}


### object-handle

# Priority 1: Use handle from smem object
sp {construct-world-object*elaborate*object-handle*from*smem-obj
   (state <s> ^name construct-world-object
              ^smem-obj.handle <obj-handle>)
-->
   (<s> ^object-handle <obj-handle>)
}

# Priority 2: Use handle from source object
sp {construct-world-object*elaborate*object-handle*from*source-obj
   (state <s> ^name construct-world-object
             -^smem-obj.handle
              ^source-obj.handle <obj-handle>)
-->
   (<s> ^object-handle <obj-handle>)
}

# Use perception id instead of handle
sp {construct-world-object*elaborate*object-handle*from*perception-obj
   (state <s> ^name construct-world-object
             -^smem-obj.handle
              ^source-type perception-obj
              ^source-obj.id <obj-handle>)
-->
   (<s> ^object-handle <obj-handle>)
}

# If there is no handle, create a new one
sp {construct-world-object*elaborate*object-handle*default-arg*create*new
   (state <s> ^name construct-world-object
              ^source-type default-arg
             -^source-obj.handle)
-->
   (<s> ^object-handle (make-constant-symbol |obj-hand|))
}

### ignore-predicate
# HARDCODED: need to be better at this
sp {construct-world-object*elaborate*ignore-predicate
   (state <s> ^name construct-world-object)
-->
   (<s> ^ignore-predicate handle item-type object-instance predicates in-smem room1 storage1 arm-status is-confirmed1 is-visible1 dialog-object waypoint timeleft)
}


