sp {op_pick-up1*magicbot*elaborate*svs-filter*node_position*object
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^target-object.svs-obj.id <obj-id>
              ^top-state.svs.command <cmd>
              ^world.robot.arm.state external)
-->
    (<cmd> ^extract <pos>)
    (<pos> ^type node_position
            ^a <obj>)
    (<obj> ^type node
           ^id <obj-id>)
    (<s> ^object-position-filter <pos>)
}

sp {op_pick-up1*magicbot*elaborate*object-position*from*filter
   (state <s> ^name op_pick-up1
              ^object-position-filter.result.record.value <p>)
-->
    (<s> ^object-position <p>)
}

sp {op_pick-up1*elaborate*svs-filter*node_position*robot
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^top-state.svs.command <cmd>)
-->
    (<cmd> ^extract <pos>)
    (<pos> ^type node_position
            ^a <obj>)
    (<obj> ^type node
           ^id robot)
    (<s> ^robot-position-filter <pos>)
}

sp {op_pick-up1*elaborate*robot-position*from*filter
   (state <s> ^name op_pick-up1
              ^robot-position-filter.result.record.value <p>)
-->
    (<s> ^robot-position <p>)
}

sp {op_pick-up1*elaborate*distance-to-object
   (state <s> ^name op_pick-up1
              ^robot-position <rpos>
              ^object-position <opos>)
   (<rpos> ^x <rx> ^y <ry>)
   (<opos> ^x <ox> ^y <oy>)
-->
   (<s> ^distance-to-object (sqrt (+ (* (- <rx> <ox>) (- <rx> <ox>)) 
                                     (* (- <ry> <oy>) (- <ry> <oy>)))))
}

 
