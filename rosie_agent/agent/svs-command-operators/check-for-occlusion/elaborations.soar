#####################################################################
#
# check-for-occlusion parameters
#
#####################################################################

# object-info <obj-info> [REQUIRED]
#   Must come from ^top-state.perception-monitor.object-monitor.object-info <obj-info>
sp {check-for-occlusion*elaborate*object-info
   (state <s> ^name check-for-occlusion
              ^superstate.operator.object-info <obj-info>)
-->
   (<s> ^object-info <obj-info>)
}

# report-to-superstate <bool> [OPTIONAL]
#   If true, adds a ^object-occluded <bool> wme to the superstate
#      and a ^occlusion-amount <num> wme to superstate range(0-1)
#   [DEFAULT-VALUE] false
sp {check-for-occlusion*elaborate*report-to-superstate*copy
   (state <s> ^name check-for-occlusion
              ^superstate.operator.report-to-superstate <bool>)
-->
   (<s> ^report-to-superstate <bool>)
}

sp {check-for-occlusion*elaborate*report-to-superstate*default
   (state <s> ^name check-for-occlusion
              ^superstate.operator <op>)
   (<op> -^report-to-superstate)
-->
   (<s> ^report-to-superstate false)
}

# report-to-top-state <bool> [OPTIONAL]
#  If true, the given obj-info will be modified to reflect 
#     an update in the occlusion-time and is-occluded variables
#  [DEFAULT-VALUE] true

sp {check-for-occlusion*elaborate*report-to-top-state*copy
   (state <s> ^name check-for-occlusion
              ^superstate.operator.report-to-top-state <bool>)
-->
   (<s> ^report-to-top-state <bool>)
}

sp {check-for-occlusion*elaborate*report-to-top-state*default
   (state <s> ^name check-for-occlusion
              ^superstate.operator <op>)
   (<op> -^report-to-top-state)
-->
   (<s> ^report-to-top-state true)
}

# Required agent-params from top-state$

sp {check-for-occlusion*elaborate*agent-params*occlusion-check-period
   (state <s> ^name check-for-occlusion
              ^agent-params.occlusion-check-period <p>)
-->
   (<s> ^occlusion-check-period <p>)
}

sp {check-for-occlusion*elaborate*agent-params*occlusion-threshold
   (state <s> ^name check-for-occlusion
              ^agent-params.occlusion-threshold <t>)
-->
   (<s> ^occlusion-threshold <t>)
}

# Candidate-set
# 2 cases: one when there is a perception object (partial occlusion, must ignore)
#			and one where there is no such perception object

sp {check-for-occlusion*propose*occlusion-command*no-perception-obj
   (state <s> ^name check-for-occlusion
              ^object-info <obj-info>)
   (<obj-info> ^belief-obj.id <id>
               ^perception-id <pid>
              -^perception-obj.id <pid>)
-->
   (<s> ^candidate-set <cand>)
   (<cand> ^type combine_nodes
           ^a <arm>
           ^a <perc-objs>)
   (<arm> ^type node
          ^id arm)
   (<perc-objs> ^type tag_select
		       ^a.type all_nodes
		       ^tag_name object-source 
		       ^tag_value perception)
}

# Make sure there is not self occlusion
sp {check-for-occlusion*propose*occlusion-command*with*perception-obj
   (state <s> ^name check-for-occlusion
              ^object-info <obj-info>)
   (<obj-info> ^belief-obj.id <id>
               ^perception-id <perc-id>
               ^perception-obj.id <perc-id>)
-->
   (<s> ^candidate-set <cand>)
   (<cand> ^type combine_nodes
           ^a <arm>
           ^a <filtered-p-objs>)
   (<arm> ^type node
          ^id arm)
   (<filtered-p-objs> ^type remove_node
                      ^id <perc-id>
                      ^a <perc-objs>)
   (<perc-objs> ^type tag_select
		       ^a.type all_nodes
		       ^tag_name object-source 
		       ^tag_value perception)
}
