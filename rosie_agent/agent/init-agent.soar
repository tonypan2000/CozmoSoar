sp {top-state*apply*init-agent*create*name
	(state <s> ^top-state <s>
				^operator.name init-agent)
-->
	(<s> ^name rosie)
}

### the agent maintains an interaction state
## initialized at agent initialization

sp {top-state*apply*init-agent*create*interaction-stack
   (state <s> ^top-state <s>
              ^operator.name init-agent)
-->
   (<s> ^interaction <int>)
   (<int> ^stack <stack>
          ^status <status>
          ^changes <changes>)
   (<stack> ^segment <seg>
            ^segments.segment <seg>)
   (<seg> ^id 10
   	  ^purpose none
	  ^counter 1)
}

# Create the perception-monitor used to compare perception to the world
sp {top-state*apply*init-agent*create*perception-monitor
   (state <s> ^top-state <s>
              ^agent-params.domain << magicbot tabletop fetch ai2thor cozmo >>
              ^operator.name init-agent
              ^io.input-link <il>
              ^svs.spatial-scene <scene>)
-->
   (<s> ^perception-monitor <mon>)
   (<mon> ^change-info <info>
          ^changes <changes>
          ^object-monitor <obj-mon>
          ^predicate-monitor <pred-mon>
          ^input-link <il>
          ^spatial-scene <scene>)
}

sp {top-state*apply*init-agent*create*perception-monitor*internal*simulate
   (state <s> ^top-state <s>
              ^agent-params.domain internal
              ^agent-params.simulate-perception true
              ^internal-world.starting-location <start>
              ^operator.name init-agent)
-->
   (<s> ^perception-monitor <mon>)
   (<mon> ^change-info <info>
          ^changes <changes>
          ^object-monitor <obj-mon>
          ^predicate-monitor <pred-mon>
          ^input-link <il>
          ^spatial-scene <scene>)
   (<il> ^objects <objs>
         ^self <self>)
   (<self> ^moving-status stopped
           ^current-waypoint.waypoint-handle <start>
           ^arm <arm>)
   (<arm> ^holding-object none
          ^moving-status wait)
}

# Creates the task-stack during init-agent
sp {top-state*apply*init-agent*create*task-stack
   (state <s> ^superstate nil
              ^operator.name init-agent)
-->
   (<s> ^task-stack <stack>)
   (<stack> ^top none
            ^bottom none
            ^changes <c>
            ^queued-tasks <q>
            ^segments <segs>)
}
              

sp {top-state*apply*init-agent*cozmo*reset*robot
   (state <s> ^superstate nil
              ^agent-params.domain cozmo
              ^operator.name init-agent
              ^io.output-link <ol>)
-->
   (<ol> ^move-lift.height 0.0
         ^move-head.angle 0.0)
}

