sp {op_put-down1*propose*op_find1
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^destination <dest>)
   (<dest> ^predicates <preds>)
   (<preds> ^is-visible1 not-visible1)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_find1
        ^item-type task-operator
        ^task-handle find1
        ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <dest>)
}

sp {op_put-down1*propose*op_approach1
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^destination <dest>)
   (<dest> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 not-reachable1)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_approach1
        ^item-type task-operator
        ^task-handle approach1
        ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <dest>)
}

sp {op_put-down1*propose*primitive-put-down*no*destination
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^held-object <obj>
             -^destination <dest>)
   (<obj> ^predicates.arm-status grabbed)
-->
   (<s> ^operator <o> +)
   (<o> ^name primitive-put-down
        ^item-type task-operator
        ^task-handle primitive-put-down1
        ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <obj>)
}

sp {op_put-down1*propose*primitive-put-down*destination
   (state <s> ^name op_put-down1
              ^problem-space.execution-type external
              ^held-object <obj>
              ^predicate-handle <pred-h>
              ^destination <dest>)
   (<obj> ^predicates.arm-status grabbed)
   (<dest> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1)
-->
   (<s> ^operator <o> +)
   (<o> ^name primitive-put-down
        ^item-type task-operator
        ^task-handle primitive-put-down1
        ^arg1 <arg1>
        ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj>)
   (<arg2> ^arg-type partial-predicate
           ^handle <pred-h>
           ^2 <dest>)
}
