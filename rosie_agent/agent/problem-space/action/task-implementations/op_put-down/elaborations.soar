sp {op_put-down1*elaborate*held-object*task-operator
   (state <s> ^name op_put-down1
              ^task-operator.arg1.id <obj>)
-->
   (<s> ^held-object <obj>)
}

sp {op_put-down1*elaborate*predicate-handle*task-operator
   (state <s> ^name op_put-down1
              ^task-operator.arg2.handle <pred-handle>)
-->
   (<s> ^predicate-handle <pred-handle>)
}

sp {op_put-down1*elaborate*destination*task-operator
   (state <s> ^name op_put-down1
              ^task-operator.arg2.{2} <obj>)
-->
   (<s> ^destination <obj>)
}

sp {op_put-down1*elaborate*destination*task-operator*current-location
   (state <s> ^name op_put-down1
              ^task-operator <task>
              ^world.objects.object <obj>)
   (<task> -^arg2)
   (<obj> ^predicates.modifier1 current1)
-->
   (<s> ^destination <obj>)
}

sp {op_put-down1*cozmo*propose*add-in-predicate
   (state <s> ^name op_put-down1
              ^agent-params.domain cozmo
              ^current-task-segment.put-down-command.status succeeded
              ^held-object <obj>
              ^world.objects.object <loc>
              ^world.predicates.predicate <in-pred>)
   (<loc> ^predicates.modifier1 current1)
   (<in-pred> ^handle in1)
   -{(<in-pred> ^instance <i>)
     (<i> ^1 <obj> ^2 <loc>)}
-->
   (<s> ^operator <o> + >)
   (<o> ^name add-in-predicate)
}

sp {op_put-down*cozmo*apply*add-in-predicate
   (state <s> ^name op_put-down1
              ^operator.name add-in-predicate
              ^held-object <obj>
              ^world.objects.object <loc>
              ^world.predicates.predicate <in-pred>)
   (<loc> ^predicates.modifier1 current1)
   (<in-pred> ^handle in1)
-->
   (<in-pred> ^instance <i>)
   (<i> ^1 <obj> ^2 <loc>)
}


