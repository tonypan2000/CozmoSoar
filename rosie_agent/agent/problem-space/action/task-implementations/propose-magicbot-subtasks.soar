
sp {composite-propose*op_give1
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj1>
              ^world.objects.object <person>)
   (<ps> ^name action
         ^propose-subtasks true)
   (<obj1> ^predicates.arm-status grabbed)
   (<person> ^predicates.is-visible1 visible1
             ^predicates.category person)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_give1
         ^item-type task-operator
         ^task-handle give1
         ^arg1 <arg1>
         ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj1>)
   (<arg2> ^arg-type partial-predicate
           ^handle to1
           ^2 <person>)
}

sp {action*subtask-proposals*magicbot*propose*op_find
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>)
   (<ps> ^name action
         ^propose-subtasks true)
   (<obj> ^predicates.is-visible1 not-visible1)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_find1
         ^item-type task-operator
         ^task-handle find1
         ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <obj>)
}

###################################################################
#
# problem-space.propose-driving-operators << true false >>
#   If true, proposes operators for driving actions
#      turn, orient, follow-wall, stop
#   These do not have action models or preconditions
#       (Only proposed when trying to match a primitive action command)
#
###################################################################


## op_turn
sp {composite-propose*driving-operator*op_turn1
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
-->
   (<s> ^operator <oleft> +
        ^operator <oright> +
        ^operator <oaround> +)
   (<oleft> ^name op_turn1
          ^item-type task-operator
          ^task-handle turn1
          ^arg1 <left-arg1>)
   (<left-arg1> ^arg-type concept ^handle left1)
   
   (<oright> ^name op_turn1
             ^item-type task-operator
             ^task-handle turn1
             ^arg1 <right-arg1>)
   (<right-arg1> ^arg-type concept ^handle right1)
   
   (<oaround> ^name op_turn1
             ^item-type task-operator
             ^task-handle turn1
             ^arg1 <around-arg1>)
   (<around-arg1> ^arg-type concept ^handle around1)
}

## op_follow-wall1
sp {composite-propose*driving-operator*op_follow-wall1
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
-->
   (<s> ^operator <left-op> +
        ^operator <right-op> +)
   (<left-op> ^name op_follow-wall1
              ^item-type task-operator
              ^task-handle follow-wall1
              ^arg1 <left-arg1>)
   (<left-arg1> ^arg-type object ^id <left-wall>)
   (<left-wall> ^item-type object ^predicates <left-preds>)
   (<left-preds> ^category object ^relative-direction1 left1 ^spatial-shape wall1)

   (<right-op> ^name op_follow-wall1
               ^item-type task-operator
               ^task-handle follow-wall1
               ^arg1 <right-arg1>)
   (<right-arg1> ^arg-type object ^id <right-wall>)
   (<right-wall> ^item-type object ^predicates <right-preds>)
   (<right-preds> ^category object ^relative-direction1 right1 ^spatial-shape wall1)
}

## op_orient1
sp {composite-propose*driving-operator*op_orient1*each*direction
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
-->
   (<s> ^operator <on> +
        ^operator <os> +
        ^operator <oe> +
        ^operator <ow> +)

   (<on> ^name op_orient1
         ^item-type task-operator
         ^task-handle orient1
         ^arg1 <n-arg1>)
   (<n-arg1> ^arg-type concept ^handle north1)

   (<os> ^name op_orient1
         ^item-type task-operator
         ^task-handle orient1
         ^arg1 <s-arg1>)
   (<s-arg1> ^arg-type concept ^handle south1)

   (<oe> ^name op_orient1
         ^item-type task-operator
         ^task-handle orient1
         ^arg1 <e-arg1>)
   (<e-arg1> ^arg-type concept ^handle east1)

   (<ow> ^name op_orient1
         ^item-type task-operator
         ^task-handle orient1
         ^arg1 <w-arg1>)
   (<w-arg1> ^arg-type concept ^handle west1)
}

## op_stop1
sp {composite-propose*driving-operator*op_stop1
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_stop1 
        ^item-type task-operator
        ^task-handle stop1)
}

## op_drive-forward1
sp {composite-propose*driving-operator*op_drive-forward1
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
-->
   (<s> ^operator <drive-op> +)
   (<drive-op> ^name op_drive-forward1
               ^item-type task-operator
               ^task-handle drive-forward1)
}

## op_scan1
sp {composite-propose*driving-operator*op_scan1
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
-->
   (<s> ^operator <scan-op> +)
   (<scan-op> ^name op_scan1
              ^item-type task-operator
              ^task-handle scan1)
}

## op_face1
sp {composite-propose*driving-operator*op_face1
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>
              ^world.robot.current-location <cur-loc>
              ^world.predicates.predicate <in-pred>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
   (<obj> ^predicates <preds>)
  (<preds> ^category << object person >>
            ^arm-status not-grabbed)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <cur-loc>)
-->
   (<s> ^operator <scan-op> +)
   (<scan-op> ^name op_face1
              ^item-type task-operator
              ^task-handle face1
              ^arg1 <obj-arg>)
   (<obj-arg> ^arg-type object
              ^id <obj>)
}

## op_go-to1 for an object
sp {propose*op_go-to1*object
   (state <s> ^problem-space <ps>
              ^world.objects.object <obj>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
  (<obj> ^predicates.category << person object >> )
-->
   (<s> ^operator <o> +)
   (<o> ^name op_go-to1
        ^item-type task-operator
        ^task-handle go-to1
         ^arg2 <arg2>)
   (<arg2> ^arg-type partial-predicate
           ^handle to1
           ^2 <obj>)
}

## op_follow-person1
sp {composite-propose*op_follow-person1
   (state <s> ^problem-space <ps>
              ^world.objects.object <person>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
  (<person> ^predicates <preds>)
  (<preds> ^category person
           ^is-visible1 visible1)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_follow-person1
         ^item-type task-operator
         ^task-handle follow-person1
         ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <person>)
}

sp {composite-propose*driving-operator*op_explore
   (state <s> ^problem-space <ps>)
   (<ps> ^name action
         ^propose-subtasks true
         ^propose-driving-operators true)
-->
   (<s> ^operator <o> +)
   (<o> ^name op_explore1
        ^item-type task-operator
        ^task-handle explore1)
}

## op_start-leading1
sp {composite-propose*op_start-leading1
   (state <s> ^problem-space <ps>
              ^world.objects.object <person>)
   (<ps> ^name action
         ^propose-subtasks true)
  (<person> ^predicates <preds>)
  (<preds> ^category person
           ^is-visible1 visible1
          -^robot-status leading)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_start-leading1
         ^item-type task-operator
         ^task-handle start-leading1
         ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <person>)
}

## op_stop-leading1
sp {composite-propose*op_stop-leading1
   (state <s> ^problem-space <ps>
              ^world.objects.object <person>)
   (<ps> ^name action
         ^propose-subtasks true)
  (<person> ^predicates <preds>)
  (<preds> ^category person
           ^robot-status leading)
-->
   (<s> ^operator <op> +)
   (<op> ^name op_stop-leading1
         ^item-type task-operator
         ^task-handle stop-leading1
         ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <person>)
}
