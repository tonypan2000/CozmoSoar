### For the internal environment, simulate the turn-on command on the top-state world ###

sp {op_turn-on1*propose*simulate-turn-on-command
   (state <s> ^name op_turn-on1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^turn-on-command <cmd>)
   (<cmd> -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-turn-on-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {op_turn-on1*apply*simulate-turn-on-command*mark*command*complete
   (state <s> ^name op_turn-on1
              ^operator <o>)
   (<o> ^name simulate-turn-on-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}


### simulate-perception = false, change on the object directly ###

sp {op_turn-on1*apply*simulate-turn-on-command*change*activation1
   (state <s> ^name op_turn-on1
              ^operator.name simulate-turn-on-command
             -^agent-params.simulate-perception true
              ^target-object.predicates <preds>)
   (<preds> ^activation1 off2)
-->
   (<preds> ^activation1 off2 -
            ^activation1 on2)
}

# If an object is in a microwave, make it hot
sp {op_turn-on1*apply*simulate-turn-on-command*microwave*make*contents*hot
   (state <s> ^name op_turn-on1
              ^operator.name simulate-turn-on
             -^agent-params.simulate-perception true
              ^target-object <mic>
              ^world <w>)
   (<mic> ^predicates.name microwave1)
   (<w> ^objects.object <obj>
        ^predicates.predicate <pred>)
   (<pred> ^handle in1
           ^instance <i>)
   (<i> ^1 <obj> ^2 <mic>)
   (<obj> ^predicates <obj-preds>)
   (<obj-preds> ^temperature { <old-temp> <> hot1 })
-->
   (<obj-preds> ^temperature <old-temp> -
                ^temperature hot1)
}


### simulate-perception = true, change on the perception-monitor ###

sp {op_turn-on1*apply*simulate-turn-on-command*simulate-perception*change*activation
   (state <s> ^name op_turn-on1
              ^operator.name simulate-turn-on-command
              ^agent-params.simulate-perception true
              ^target-object <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^wm-obj <obj>)
   (<il-prop> ^property-handle activation1
              ^values <vals>)
-->
   (<vals> ^off2 1.00 -
           ^on2 1.00)
}

sp {op_turn-on1*apply*simulate-turn-on-command*simulate-perception*set-timer
   (state <s> ^name op_turn-on1
              ^operator <o>
              ^agent-params.simulate-perception true
              ^target-object <obj>
              ^io.input-link.time.clock.elapsed-seconds <secs>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<o> ^name simulate-turn-on-command
        ^command <cmd>)
   (<cmd> ^name set-timer
          ^time <t>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^wm-obj <obj>)
   (<il-prop> ^property-handle activation1)
-->
   (<il-prop> ^turn-off-time (+ <secs> (* <t> 10)))
}

# If an object is in a microwave, make it hot
sp {op_turn-on1*apply*simulate-turn-on-command*simulate-perception*microwave*make*hot
   (state <s> ^name op_turn-on1
              ^operator.name simulate-turn-on
              ^agent-params.simulate-perception true
              ^target-object <mic>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^world <w>)
   (<mic> ^predicates.name microwave1)
   (<w> ^objects.object <obj>
        ^predicates.predicate <pred>)
   (<pred> ^handle in1
           ^instance <i>)
   (<i> ^1 <obj> ^2 <mic>)
   (<obj-info> ^wm-obj <obj>
               ^input-link-obj.property <il-prop>)
   (<il-prop> ^property-handle temperature
              ^values <vals>)
   (<vals> ^{ <prop> <> hot1 } 1.00)
-->
   (<vals> ^<prop> 1.00 -
           ^hot1 1.00)
}


