# elaborate problem-space attend-to-all-perception
sp {op_scan1*elaborate*problem-space*attend-to-all-perception*yes
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_scan1*elaborate*executing-command*true
   (state <s> ^name op_scan1
              ^current-task-segment.{ << stop-command turn-command >> }.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_scan1*elaborate*propose-wait*true
   (state <s> ^name op_scan1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}

#### GOAL SATISFACTION #####
sp {op_scan*elaborate*desired*satisfied*no*until-clause
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^current-task-segment.turn-command.status success
              -^until-clause)
-->
   (<s> ^desired.satisfied true)
}

sp {op_scan*elaborate*desired*satisfied*until-clause
   (state <s> ^name op_scan1
              ^problem-space.execution-type external
              ^current-task-segment.stop-command.status success
              ^until-clause.satisfied true)
-->
   (<s> ^desired.satisfied true)
}
