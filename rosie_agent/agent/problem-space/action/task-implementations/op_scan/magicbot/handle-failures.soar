
#### Different failure cases that can arise during scan

sp {op_scan1*propose*handle-execution-failure*stop
   (state <s> ^name op_scan1
              ^current-task-segment <seg>)
   (<seg> ^stop-command <cmd>)
   (<cmd> ^status << failure interrupted >>)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name handle-execution-failure
        ^category failure-handling
        ^task-segment <seg>
        ^command <cmd>)
}

sp {op_scan1*propose*handle-execution-failure*turn
   (state <s> ^name op_scan1
              ^current-task-segment <seg>)
   (<seg> ^turn-command <cmd>)
   (<cmd> ^status << failure >>) # Turn can be interrupted
-->
   (<s> ^operator <o> + = >)
   (<o> ^name handle-execution-failure
        ^category failure-handling
        ^task-segment <seg>
        ^command <cmd>)
}

# unsatisifed-goal: the scan command finished without the until clause being satisfied
sp {op_scan1*propose*handle-execution-failure*unsatisifed-goal*after*scan
   (state <s> ^name op_scan1
              ^current-task-segment <seg>
             -^parent-task-segment.task-operator.task-handle explore1
              ^until-clause <until>)
   (<seg> ^turn-command.status success)
   (<until> -^satisfied true)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name handle-execution-failure
        ^subtype unsatisfied-goal
        ^category failure-handling
        ^task-segment <seg>)
}

sp {op_scan1*propose*handle-execution-failure*unsatisifed-goal*after*scan*explore*silent
   (state <s> ^name op_scan1
              ^current-task-segment <seg>
              ^parent-task-segment.task-operator.task-handle explore1
              ^until-clause <until>)
   (<seg> ^turn-command.status success)
   (<until> -^satisfied true)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name handle-execution-failure
        ^subtype unsatisfied-goal
        ^silent true
        ^category failure-handling
        ^task-segment <seg>)
}

sp {op_scan1*propose*handle-command-failure
   (state <s> ^name op_scan1
              ^current-task-segment <seg>)
   (<seg> ^{ << turn-command stop-command >> } <cmd>)
   (<cmd> ^status error)
-->
   (<s> ^operator <o> + = >)
   (<o> ^name handle-command-failure
        ^category failure-handling
        ^task-segment <seg>
        ^command <cmd>)
}

