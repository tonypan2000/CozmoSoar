### send the low-level face command to the output-link when the until-clause is satisfied ###

sp {op_face1*propose*send-face-command
   (state <s> ^name op_face1
              ^target-coord)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-face-command
        ^item-type command-operator
        ^command-name face-command)
}


### cozmo ###

#sp {op_face1*external*apply*send-face-command*cozmo
#   (state <s> ^name op_face1
#              ^problem-space.execution-type external
#              ^operator <o>
#              ^agent-params.domain cozmo
#              ^target-object <obj>
#              ^current-task-segment <seg>
#              ^top-state.io.output-link <out>)
#   (<o> ^name send-face-command
#        ^command-name <cmd-name>)
#-->
#   (<out> ^face-forward <df>)
#   (<df> ^item-type output-command
#         ^distance 0.3
#         ^speed 0.1)
#   (<seg> ^<cmd-name> <df>)
#}

sp {op_face1*external*apply*send-face-command*cozmo*coord
   (state <s> ^name op_face1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^target-coord <coord>
              ^current-task-segment <seg>
              ^io.input-link.self.pose <pose>
              ^top-state.io.output-link <out>)
   (<o> ^name send-face-command
        ^command-name <cmd-name>)
   (<pose> ^x <x> ^y <y>)
   (<coord> ^x <cx> ^y <cy>)
-->
   (<out> ^turn-in-place <tip>)
   (<tip> ^item-type output-command
          ^absolute true
          ^angle (atan2 (- <cy> <y>) (- <cx> <x>) )
          ^speed 0.75)
   (<seg> ^<cmd-name> <tip>)
}

sp {op_face1*external*apply*send-face-command*magicbot*orient
   (state <s> ^name op_face1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain magicbot
              ^yaw-to-target <yaw>
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-face-command
        ^command-name <cmd-name>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^item-type output-command
          ^name orient
          ^parameters.yaw <yaw>
          ^termination-condition.name stabilized)
   (<seg> ^<cmd-name> <law>)
}

sp {op_face1*external*apply*send-face-command*magicbot*target-coord
   (state <s> ^name op_face1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain magicbot
              ^target-coord <target>
              ^current-task-segment <seg>
              ^io.input-link.self.pose <pose>
              ^top-state.io.output-link <out>)
   (<o> ^name send-face-command
        ^command-name <cmd-name>)
   (<pose> ^x <rx> ^y <ry>)
   (<target> ^x <tx> ^y <ty>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^item-type output-command
          ^name orient
          ^parameters.yaw (atan2 (- <ty> <ry>) (- <tx> <rx>))
          ^termination-condition.name stabilized)
   (<seg> ^<cmd-name> <law>)
}
