### Send the low-level open command when the door is visible and reachable ###

sp {op_open1*propose*send-open-command
   (state <s> ^name op_open1
              ^target-object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1
            ^door2 closed2)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-open-command
        ^item-type command-operator
        ^command-name open-command
        ^object <obj>)
}


### model send-open-command ###

sp {op_open1*internal*apply*send-open-command*change*door
   (state <s> ^name op_open1
              ^problem-space.execution-type internal
              ^operator <o>)
   (<o> ^name send-open-command
        ^object.predicates <preds>)
   (<preds> ^door2 closed2)
-->
   (<preds> ^door2 closed2 -
            ^door2 open2)
}


### ai2thor and internal ###

sp {op_open1*external*apply*send-open-command*ai2thor*internal
   (state <s> ^name op_open1
              ^problem-space.execution-type external
              ^agent-params.domain << ai2thor internal >>
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-open-command
        ^command-name <cmd-name>
        ^object.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name open
         ^object <h>)
   (<seg> ^<cmd-name> <pa>)
}


### tabletop ###

sp {op_open1*external*apply*send-open-command*tabletop
   (state <s> ^name op_open1
              ^problem-space.execution-type external
              ^agent-params.domain tabletop
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-open-command
        ^command-name <cmd-name>
        ^object.handle <h>)
-->
   (<ol> ^set-state <set>)
   (<set> ^object-handle <h>
          ^name door2
          ^value open2)
   (<seg> ^<cmd-name> <set>)
}
