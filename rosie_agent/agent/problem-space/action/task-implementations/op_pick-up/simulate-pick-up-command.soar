sp {execute*op_pick-up1*propose*simulate-pick-up-command
   (state <s> ^name op_pick-up1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^pick-up-command <cmd>)
   (<cmd> ^name pick-up
          ^object <obj>
         -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-pick-up-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {execute*op_pick-up1*apply*simulate-pick-up-command*mark*command*complete
   (state <s> ^name op_pick-up1
              ^operator <o>)
   (<o> ^name simulate-pick-up-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}

sp {execute*op_pick-up1*apply*simulate-pick-up-command*change*holding-object
   (state <s> ^name op_pick-up1
              ^operator.name simulate-pick-up-command
              ^agent-params.simulate-perception true
              ^target-object <obj>
              ^top-state.perception-monitor.input-link.self.arm <arm>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^wm-obj <obj>
               ^perception-id <pid>)
   (<arm> ^holding-object { <old> <> <pid> })
-->
   (<arm> ^holding-object <old> -
          ^holding-object <pid>)
}

sp {execute*op_pick-up1*apply*simulate-pick-up-command*remove*predicates
   (state <s> ^name op_pick-up1
              ^operator.name simulate-pick-up-command
              ^target-object <obj>
              ^world.predicates.predicate <pred>)
   (<pred> ^instance <i>)
   (<i> ^{ << 1 2 >> } <obj>)
-->
   (<pred> ^instance <i> -)
}

sp {execute*op_pick-up1*apply*simulate-pick-up-command*add*arm-status*grabbed
   (state <s> ^name op_pick-up1
              ^operator.name simulate-pick-up-command
             -^agent-params.simulate-perception true
              ^target-object.predicates <preds>)
   (<preds> ^arm-status not-grabbed)
-->
   (<preds> ^arm-status not-grabbed -
            ^arm-status grabbed)
}
