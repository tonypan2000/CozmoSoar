sp {primitive-pick-up*internal*propose*perform-pickup
   (state <s> ^name primitive-pick-up
              ^agent-params.domain internal
              ^problem-space.execution-type external
              ^target-object.predicates.arm-status not-grabbed)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-pickup)
}

sp {primitive-pick-up*internal*apply*perform-pickup*change*holding-object
   (state <s> ^name primitive-pick-up
              ^agent-params.domain internal
              ^problem-space.execution-type external
              ^operator.name perform-pickup
              ^top-state.perception-monitor.input-link.self.arm <arm>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^target-object <obj>)
   (<obj-info> ^wm-obj <obj>
               ^perception-id <pid>)
   (<arm> ^holding-object { <old> <> <pid> })
-->
   (<arm> ^holding-object <old> -
          ^holding-object <pid>)
}

sp {primitive-pick-up*internal*prefer*attend-to-scene-change*over*perform-pick-up
   (state <s> ^name primitive-pick-up
              ^agent-params.domain internal
              ^operator <o1> +
              ^operator <o2> +)
   (<o1> ^name attend-to-scene-change)
   (<o2> ^name perform-pickup)
-->
   (<s> ^operator <o1> > <o2>)
}

