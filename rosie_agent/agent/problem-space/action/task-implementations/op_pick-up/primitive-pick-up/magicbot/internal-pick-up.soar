sp {primitive-pick-up*magicbot*propose*internal-pick-up
   (state <s> ^name primitive-pick-up
              ^agent-params.domain magicbot
              ^problem-space.execution-type external
              ^target-object <obj>
              ^top-state.world.robot.arm.state internal
              -^current-task-segment.pick-up-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name internal-pick-up
        ^object <obj>)
}

#sp {primitive-pick-up*magicbot*apply*internal-pick-up*send-confirmation
#   (state <s> ^name primitive-pick-up
#              ^agent-params.domain magicbot
#              ^operator <o>
#              ^current-task-segment.received-yes true
#              ^top-state.interaction.changes <c>)
#   (<o> ^name internal-pick-up)
#-->
#   (<c> ^outgoing-message <msg>)
#   (<msg> ^type confirm-pick-up
#          ^complete-message true)
#}

sp {primitive-pick-up*magicbot*apply*internal-pick-up*send-message
   (state <s> ^name primitive-pick-up
              ^agent-params.domain magicbot
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-pick-up
        ^object <obj>)
   (<seg> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type get-item-request
          ^item <obj>)
   (<seg> ^sent-message true)
}

sp {primitive-pick-up*magicbot*apply*internal-pick-up*mark*success
   (state <s> ^name primitive-pick-up
              ^agent-params.domain magicbot
              ^operator.name internal-pick-up
              ^target-object.predicates <preds>
              ^current-task-segment <seg>)
   (<seg> ^received-yes true)
-->
   (<seg> ^pick-up-command.status success)
  (<preds> ^arm-status grabbed
           ^arm-status not-grabbed -)
}

sp {primitive-pick-up*magicbot*apply*internal-pick-up*delete*belief*obj
   (state <s> ^name primitive-pick-up
              ^agent-params.domain magicbot
              ^operator.name internal-pick-up
              ^current-task-segment.received-yes
              ^target-object.svs-obj.id <id>
              ^top-state.svs.command <cmd>)
-->
   (<cmd> ^delete_node.id <id>)
}


