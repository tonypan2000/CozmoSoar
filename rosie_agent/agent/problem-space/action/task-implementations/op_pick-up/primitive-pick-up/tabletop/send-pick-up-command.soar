sp {primitive-pick-up*tabletop*propose*send-pick-up-command
   (state <s> ^name primitive-pick-up
              ^agent-params.domain tabletop
              ^problem-space.execution-type external
              ^target-object <obj>
              ^world.robot.arm.moving-status wait
             -^current-task-segment.pick-up-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name send-pick-up-command
        ^object <obj>)
}

sp {primitive-pick-up*tabletop*apply*send-pick-up-command*send
   (state <s> ^name primitive-pick-up
              ^agent-params.domain tabletop
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-pick-up-command
		^object.handle <obj-handle>)
-->
   (<ol> ^pick-up <cmd>)
   (<cmd> ^object-handle <obj-handle>)
   (<seg> ^pick-up-command <cmd>)
}

sp {primitive-pick-up*tabletop*apply*send-pick-up-command*mark*object*moving
   (state <s> ^name primitive-pick-up
              ^agent-params.domain tabletop
              ^operator <o>)
   (<o> ^name send-pick-up-command
        ^object.predicates <preds>)
-->
   (<preds> ^arm-status moving)
}

