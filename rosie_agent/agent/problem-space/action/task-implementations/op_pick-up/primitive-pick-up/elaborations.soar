sp {primitive-pick-up*external*elaborate*problem-space*attend-to-all-perception
   (state <s> ^name primitive-pick-up
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {primitive-pick-up*elaborate*target-object*superoperator
   (state <s> ^name primitive-pick-up
              ^task-operator.arg1.id <obj>)
-->
   (<s> ^target-object <obj>)
}

sp {primitive-pick-up*elaborate*object-info*perception-monitor
   (state <s> ^name primitive-pick-up
              ^problem-space.execution-type external
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>
              ^target-object <obj>)
   (<obj-info> ^wm-obj <obj>)
-->
   (<s> ^object-info <obj-info>)
}

sp {primitive-pick-up*elaborate*executing-command*true
   (state <s> ^name primitive-pick-up
              ^current-task-segment.pick-up-command.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {primitive-pick-up*elaborate*propose-wait*true
   (state <s> ^name primitive-pick-up
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}
