### direction-handle ###
# The direction of the wall to follow along (defaults to right1)

sp {op_orient1*elaborate*direction-handle*from*task-operator
   (state <s> ^name op_orient1
              ^task-operator.arg1.id.predicates.relative-direction1 <dir-handle>)
-->
   (<s> ^direction-handle <dir-handle>)
}

sp {op_orient1*elaborate*direction-handle*default
   (state <s> ^name op_orient1
             -^direction-handle <> right1)
-->
   (<s> ^direction-handle right1)
}


### direction-id ###
# The smem LTI with info about the direction to orient towards

sp {op_orient1*elaborate*smem-query*lookup*direction-handle
   (state <s> ^name op_orient1
			     ^direction-handle <dir-handle>)
-->
   (<s> ^smem-query <q>)
   (<q> ^cue <cue>
        ^depth 2)
	(<cue> ^handle <dir-handle>)
}

sp {op_orient1*elaborate*direction-id*from*smem-query*result
   (state <s> ^name op_orient1
              ^direction-handle <dir-handle>
              ^smem-query.result <dir-id>)
   (<dir-id> ^handle <dir-handle>)
-->
   (<s> ^direction-id <dir-id>)
}


#### GOAL SATISFACTION ####

sp {op_follow-wall1*elaborate*desired*satisfied*no*until-clause
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              ^current-task-segment.follow-wall-command.status success
             -^task-operator.until-clause)
-->
   (<s> ^desired.satisfied true)
}

sp {op_follow-wall1*elaborate*desired*satisfied*until-clause
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
              ^current-task-segment.stop-command.status success
              ^task-operator.until-clause.satisfied true)
-->
   (<s> ^desired.satisfied true)
}

sp {op_follow-wall1*elaborate*desired*satisfied*until-clause*no*command*sent
   (state <s> ^name op_follow-wall1
              ^problem-space.execution-type external
             -^current-task-segment.follow-wall-command
              ^task-operator.until-clause.satisfied true)
-->
   (<s> ^desired.satisfied true)
}
