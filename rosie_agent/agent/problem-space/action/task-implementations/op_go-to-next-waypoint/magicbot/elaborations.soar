sp {op_go-to-next-waypoint1*elaborate*problem-space*attend-to-all-perception*yes
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^problem-space.perception <p>)
-->
   (<p> ^attend-to-all-perception yes)
}

sp {op_go-to-next-waypoint1*elaborate*executing-command*true
   (state <s> ^name op_go-to-next-waypoint1
              ^current-task-segment.{ << drive-command face-command >> }.status << sent executing >>)
-->
   (<s> ^executing-command true)
}

sp {op_go-to-next-waypoint1*elaborate*propose-wait*true
   (state <s> ^name op_go-to-next-waypoint1
              ^executing-command true)
-->
   (<s> ^propose-wait true)
}

#### GOAL SATISFACTION ####
sp {op_go-to-next-waypoint1*elaborate*desired*satisfied
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^current-task-segment.drive-command.status success
              ^current-task-segment.position-set.next.next none)
-->
   (<s> ^desired.satisfied true)
}


sp {op_go-to-next-waypoint1*elaborate*waypoint
   (state <s> ^name op_go-to-next-waypoint1
              ^problem-space.execution-type external
              ^task-operator.arg1 <arg1>)
   (<arg1> ^arg-type waypoint
           ^id <wp>)
-->
   (<s> ^waypoint <wp>)
}

# Note: in the real world this should always be true,
# During development we can save time by removing this
sp {op_go-to-next-waypoint1*elaobrate*do-face
  (state <s> ^name op_go-to-next-waypoint1
             ^problem-space.execution-type external)
-->
  (<s> ^do-face false)
}

# Note: in the real world this should always be false
# During development we can save time by removing this
sp {op_go-to-next-waypoint1*elaobrate*ignore-doors
  (state <s> ^name op_go-to-next-waypoint1
             ^problem-space.execution-type external)
-->
  (<s> ^ignore-doors true)
}


