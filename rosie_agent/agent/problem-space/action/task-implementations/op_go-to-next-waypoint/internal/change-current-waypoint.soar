sp {op_go-to-next-waypoint1*propose*change-current-waypoint
   (state <s> ^name op_go-to-next-waypoint1
              ^waypoint <wp>
              ^world.robot.current-waypoint <> <wp>)
-->
   (<s> ^operator <o> +)
   (<o> ^name change-current-waypoint
        ^waypoint <wp>)
}

sp {op_go-to-next-waypoint*apply*change-current-waypoint
   (state <s> ^name op_go-to-next-waypoint1
              ^operator <o>
              ^top-state.perception-monitor.input-link.self.current-waypoint <cur-wp>)
   (<cur-wp> ^waypoint-handle <old-wp>)
   (<o> ^name change-current-waypoint
        ^waypoint.handle <wph>)
-->
   (<cur-wp> ^waypoint-handle <old-wp> -
             ^waypoint-handle <wph>)
}

sp {op_go-to-next-waypoint1*apply*change-current-waypoint*change*grabbed*obj*location
   (state <s> ^name op_go-to-next-waypoint1
              ^operator.name change-current-waypoint
              ^top-state.world.objects.object <obj>
              ^internal-world.objects.object <iw-obj>
              ^waypoint.handle <wp-h>)
   (<iw-obj> ^handle <h>
             ^waypoint { <old-wp> <> <wp-h> })
   (<obj> ^handle <h>
          ^predicates.arm-status grabbed)

-->
   (<iw-obj> ^waypoint <old-wp> -
             ^waypoint <wp-h>)
}

