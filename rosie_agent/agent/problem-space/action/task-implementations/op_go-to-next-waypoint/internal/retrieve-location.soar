# Tries to find a location in semantic memory matching the given waypoint
sp {op_go-to-next-waypoint1*retrieve-location*elaborate*smem-query
   (state <s> ^name op_go-to-next-waypoint1
              ^waypoint.handle <wp-handle>
             -^retrieved-location)
-->
   (<s> ^smem-query <query>)
   (<query> ^cue <cue>
            ^depth 2)
   (<cue> ^object-instance true
          ^waypoint <wp-handle>)
}

# Success - mark retrieved-location <lti> and new-location <lti>
sp {op_go-to-next-waypoint1*retrieve-location*success*copy*location
   :o-support
   (state <s> ^name op_go-to-next-waypoint1
              ^waypoint.handle <wp-handle>
              ^smem-query <q>)
   (<q> ^cue.waypoint <wp-handle>
        ^result { <loc> <> failure })
-->
   (<s> ^retrieved-location <loc>)
}

# Failure - mark retrieved-location none and new-location none
sp {op_go-to-next-waypoint1*retrieve-location*failure*copy*location
   :o-support
   (state <s> ^name op_go-to-next-waypoint1
              ^waypoint.handle <wp-handle>
              ^smem-query <q>)
   (<q> ^cue.waypoint <wp-handle>
        ^result failure)
-->
   (<s> ^retrieved-location none)
}
