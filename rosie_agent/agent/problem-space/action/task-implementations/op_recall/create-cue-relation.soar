sp {op_recall1*propose*create-cue-relation
  (state <s> ^name op_recall1
             ^epmem-cue <cue>
             ^query-relation <qr>)
  (<qr> ^source <src>
        -^cue)
  (<src> ^{1}.cue <obj1>
         ^{2}.cue <obj2>)
-->
  (<s> ^operator <o> + =)
  (<o> ^name create-cue-relation
       ^relation <qr>)
}

sp {op_recall1*apply*create-cue-relation*create*predicate
  (state <s> ^name op_recall1
             ^operator <o>
             ^epmem-cue.world.predicates <preds>)
  (<o> ^name create-cue-relation
       ^relation.source.handle <handle>)
  (<preds> -^predicate.handle <handle>)
-->
  (<preds> ^predicate <pred>)
  (<pred> ^handle <handle>)
}

sp {op_recall1*apply*create-cue-relation*create*instance
  (state <s> ^name op_recall1
             ^operator <o>
             ^epmem-cue.world.predicates.predicate <pred>)
  (<o> ^name create-cue-relation
       ^relation <rel>)
  (<rel> ^source <src>)
  (<src> ^handle <handle>
         ^{1}.cue <obj1>
         ^{2}.cue <obj2>)
  (<pred> ^handle <handle>)
-->
  (<pred> ^instance <i>)
  (<i> ^1 <obj1> ^2 <obj2>)
  (<rel> ^cue <i>)
}



