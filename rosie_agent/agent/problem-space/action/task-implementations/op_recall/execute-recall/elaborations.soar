#sp {op_recall*interrupt
#   (state <s> ^problem-space.name execute-primitive-action
#              ^task-handle recall1)
#-->
#   (interrupt)
#}

sp {op_recall*elaborate*epmem-query
   (state <s> ^name op_recall1)
-->
   (<s> ^epmem-query <q>)
   (<q> ^world <w>)
   (<w> ^objects <objs>
        ^predicates <preds>)
}

sp {op_recall*elaborate*epmem-query*object*arg
   (state <s> ^name op_recall1
              ^task-operator.<arg-name> <arg>
              ^epmem-query.world.objects <objs>)
   (<arg> ^arg-type object
          ^id <obj>)
-->
   (<s> ^deep-copy-object <dco>)
   (<dco> ^source <obj>
          ^copy <new-obj>)
   (<new-obj> ^predicates <preds>)
   (<objs> ^object <new-obj>)
}

sp {op_recall*elaborate*epmem-query*deep-copy-object*predicates
   (state <s> ^name op_recall1
              ^deep-copy-object <dco>)
   (<dco> ^source.predicates <src-preds>
          ^copy.predicates <copy-preds>)
   (<src-preds> ^{ <prop-name> << modifier1 category name shape property >> } <pred-name>)
-->
   (<copy-preds> ^<prop-name> <pred-name>)
}
   
sp {op_recall*elaborate*epmem-query*predicate*arg
   (state <s> ^name op_recall1
              ^task-operator.<arg1-name> <arg1>
              ^task-operator.<arg2-name> <arg2>
              ^epmem-query.world.objects <objs>
              ^epmem-query.world.predicates <preds>
              ^deep-copy-object <dco1>)
   (<arg1> ^arg-type object
           ^id <obj1>)
   (<arg2> ^arg-type partial-predicate
           ^handle <pred-handle>
           ^2 <obj2>)
   (<dco1> ^source <obj1>
           ^copy <new-obj1>)
-->
   (<s> ^deep-copy-object <dco2>)
   (<dco2> ^source <obj2>
           ^copy <new-obj2>)
   (<new-obj2> ^predicates <preds2>)
   (<objs> ^object <new-obj2>)
   (<preds> ^predicate <new-pred>)
   (<new-pred> ^handle <pred-handle>
               ^instance <i>)
   (<i> ^1 <new-obj1> ^2 <new-obj2>)
}
   
