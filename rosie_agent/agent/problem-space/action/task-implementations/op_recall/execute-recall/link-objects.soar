sp {op_recall*propose*link-objects*handle*match
   (state <s> ^name op_recall1
              ^deep-copy-object <dco>
              ^world.objects.object <src-obj>)
   (<dco> ^source <obj>
          ^epmem.handle <obj-handle>)
   (<src-obj> ^handle <obj-handle>)
   (<obj> -^linked-to <src-obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name link-objects
        ^object <obj>
        ^source <src-obj>)
}

sp {op_recall*propose*link-objects*property*match
   (state <s> ^name op_recall1
              ^deep-copy-object <dco>
              ^world.objects.object { <src-obj> <> <obj> }
             -^world.objects.object.handle <obj-handle>)
   (<dco> ^source <obj>
          ^copy <copy>
          ^epmem.handle <obj-handle>)
   (<copy> ^predicates <copy-preds>)
   (<src-obj> ^predicates <src-preds>)
   -{(<copy-preds> ^<prop-name> <pred-name>)
     (<src-preds> -^<prop-name> <pred-name>)}
   (<obj> -^linked-to <src-obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name link-objects
        ^object <obj>
        ^source <src-obj>)
}
   
sp {op_recall*apply*link-objects
   (state <s> ^name op_recall1
              ^operator <o>)
   (<o> ^name link-objects
        ^object <obj>
        ^source <src-obj>)
-->
   (<obj> ^linked-to <src-obj>)
}
