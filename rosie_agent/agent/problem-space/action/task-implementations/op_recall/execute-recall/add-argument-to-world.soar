sp {op_recall*propose*add-argument-to-world*object
   (state <s> ^name op_recall1
              ^task-operator.<arg-name> <arg>
              ^retrieved-episode { <ep> <> none }
              ^world.objects <objs>)
   (<arg> ^arg-type object
          ^id <obj>)
   (<objs> -^object <obj>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-argument-to-world
        ^arg-type object
        ^object <obj>)
}

sp {op_recall*propose*add-argument-to-world*predicate
   (state <s> ^name op_recall1
              ^task-operator.<arg1-name> <arg1>
              ^task-operator.<arg2-name> <arg2>
              ^retrieved-episode { <ep> <> none }
              ^world.objects <objs>
              ^world.predicates.predicate <pred>)
   (<arg1> ^arg-type object
           ^id <obj1>)
   (<arg2> ^arg-type partial-predicate
           ^handle <pred-handle>
           ^2 <obj2>)
   (<pred> ^handle <pred-handle>)
   -{(<pred> ^instance <i>)
     (<i> ^1 <obj1> ^2 <obj2>)}
-->
   (<s> ^operator <o> + =)
   (<o> ^name add-argument-to-world
        ^arg-type predicate
        ^pred-handle <pred-handle>
        ^1 <obj1>
        ^2 <obj2>)
}
              
sp {op_recall*apply*add-argument-to-world*object
   (state <s> ^name op_recall1
              ^operator <o>
              ^world.objects <objs>)
   (<o> ^name add-argument-to-world
        ^arg-type object
        ^object <obj>)
-->
   (<objs> ^object <obj>)
}

sp {op_recall*apply*add-argument-to-world*predicate
   (state <s> ^name op_recall1
              ^operator <o>
              ^world.predicates.predicate <pred>
              ^world.objects <objs>)
   (<o> ^name add-argument-to-world
        ^arg-type predicate
        ^pred-handle <pred-handle>
        ^1 <obj1>
        ^2 <obj2>)
   (<pred> ^handle <pred-handle>)
-->
   (<pred> ^instance <i>)
   (<i> ^1 <obj1> ^2 <obj2>)
   (<objs> ^object <obj2>)
}

sp {op_recall*apply*add-argument-to-world*object*elaborate*added*to*world
   (state <s> ^name op_recall1
              ^operator <o>
              ^parent-task-segment <seg>)
   (<o> ^name add-argument-to-world
        ^arg-type object
        ^object <obj>)
-->
   (<seg> ^added <obj>)
}

sp {op_recall*apply*add-argument-to-world*predicate*elaborate*added*to*world
   (state <s> ^name op_recall1
              ^operator <o>
              ^parent-task-segment <seg>)
   (<o> ^name add-argument-to-world
        ^arg-type predicate
        ^2 <obj>)
-->
   (<seg> ^added <obj>)
}
