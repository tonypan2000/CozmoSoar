### For the internal environment, simulate the turn-off command on the top-state world ###

sp {op_turn-off1*propose*simulate-turn-off-command
   (state <s> ^name op_turn-off1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^turn-off-command <cmd>)
   (<cmd> -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-turn-off-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {op_turn-off1*apply*simulate-turn-off-command*mark*command*complete
   (state <s> ^name op_turn-off1
              ^operator <o>)
   (<o> ^name simulate-turn-off-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}


### simulate-perception = false, change on the object directly ###

sp {op_turn-off1*apply*simulate-turn-off-command*change*activation1
   (state <s> ^name op_turn-off1
              ^operator.name simulate-turn-off-command
             -^agent-params.simulate-perception true
              ^target-object.predicates <preds>)
   (<preds> ^door2 closed2)
-->
   (<preds> ^door2 closed2 -
            ^door2 turn-off2)
}


### simulate-perception = true, change on the perception-monitor ###

sp {op_turn-off1*apply*simulate-turn-off-command*simulate-perception*change*activation
   (state <s> ^name op_turn-off1
              ^operator.name simulate-turn-off-command
              ^agent-params.simulate-perception true
              ^target-object <obj>
              ^top-state.perception-monitor.object-monitor.object-info <obj-info>)
   (<obj-info> ^input-link-obj.property <il-prop>
               ^wm-obj <obj>)
   (<il-prop> ^property-handle activation1
              ^values <vals>)
-->
   (<vals> ^on2 1.00 -
           ^off2 1.00)
}


