### Internally modeling the go-to1 task ###

# add the new current-location and mark it as visible
sp {problem-space*action*internal*apply*op_go-to-location1*add*new*location
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.robot <robot>)
   (<o> ^name op_go-to-location1
        ^arg2.{2} <new-loc>)
   (<new-loc> ^predicates <preds>)
   (<robot> ^current-location { <old-loc> <> <new-loc> })
-->
   (<robot> ^current-location <new-loc>)
   (<preds> ^is-visible1 not-visible1 -
            ^is-visible1 visible1)
}

# remove the old current-location and mark it not visible
sp {problem-space*action*internal*apply*op_go-to-location1*remove*old*location
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world.robot <robot>)
   (<o> ^name op_go-to-location1
        ^arg2.{2} <new-loc>)
   (<robot> ^current-location { <old-loc> <> <new-loc> })
   (<old-loc> ^predicates <preds>)
-->
   (<robot> ^current-location <old-loc> -)
   (<preds> ^is-visible1 visible1 -
            ^is-visible1 not-visible1)
}

# Any objects that are in the new location are marked as visible
sp {problem-space*action*internal*apply*op_go-to-location1*mark*new*objs*visible
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world <world>)
   (<o> ^name op_go-to-location1
        ^arg2.{2} <new-loc>)
   (<world> ^predicates.predicate <in-pred>
            ^objects.object <obj>)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 <new-loc>)
   (<obj> ^predicates <preds>)
-->
   (<preds> ^is-visible1 not-visible1 -
            ^is-visible1 visible1)
}

# Any objects that are in the old location are marked as not visible
sp {problem-space*action*internal*apply*op_go-to-location1*mark*old*objs*not-visible
   (state <s> ^problem-space.name action
              ^problem-space.execution-type internal
              ^operator <o>
              ^world <world>)
   (<o> ^name op_go-to-location1
        ^arg2.{2} <new-loc>)
   (<world> ^predicates.predicate <in-pred>
            ^objects.object <obj>)
   (<in-pred> ^handle in1
              ^instance <i>)
   (<i> ^1 <obj> ^2 { <old-loc> <> <new-loc> })
   (<obj> ^predicates <preds>)
-->
   (<preds> ^is-visible1 visible1 -
            ^is-visible1 not-visible1)
}

