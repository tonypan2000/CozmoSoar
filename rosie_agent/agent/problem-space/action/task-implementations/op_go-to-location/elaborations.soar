### desired-location ###
# If arg2 is a location, then elaborate it as desired-location

sp {op_go-to-location1*elaborate*desired-location
   (state <s> ^name op_go-to-location1
              ^task-operator.arg2 <arg2>)
   (<arg2> ^arg-type partial-predicate
           ^2 <loc>)
   (<loc> ^predicates.category location)
-->
   (<s> ^desired-location <loc>)
}

### desired-waypoint ###
# The waypoint the robot should go to

# waypoint comes from the desired-location
sp {op_go-to*elaborate*desired-waypoint
   (state <s> ^name op_go-to-location1
              ^desired-location.waypoint <wp-handle>
              ^top-state.maps.map.waypoint <wp>)
   (<wp> ^handle <wp-handle>)
-->
   (<s> ^desired-waypoint <wp>)
}


### GOAL SASTISFACTION ###

sp {op_go-to-location1*elaborate*desired*empty
   (state <s> ^name op_go-to-location1
              ^desired-location <loc>)
-->
   (<s> ^desired <des>)
}

sp {op_go-to-location1*elaborate*desired*satisfied
   (state <s> ^name op_go-to-location1
              ^desired-location <loc>
              ^desired <des>
              ^world.robot.current-location <loc>)
-->
   (<des> ^satisfied true)
}

