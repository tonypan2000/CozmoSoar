sp {op_write1*propose*send-write-command
   (state <s> ^name op_write1
              ^target-object <obj>
              ^target-value <val>)
   (<obj> ^predicates <preds>)
   (<preds> -^value <val>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-write-command
        ^item-type command-operator
        ^object <obj>
        ^value <val>)
}

sp {op_write1*reject*send-write-command*already*sent
   (state <s> ^name op_write1
              ^operator <o> +
              ^current-task-segment.write-command)
   (<o> ^name send-write-command)
-->
   (<s> ^operator <o> -)
}

### model send-write-command
sp {op_write1*internal*apply*send-write-command*add*value
   (state <s> ^name op_write1
              ^problem-space.execution-type internal
              ^operator <o>)
   (<o> ^name send-write-command
        ^object.predicates <preds>
        ^value <val>)
-->
   (<preds> ^value <val>)
}

# external 
sp {op_write1*external*apply*send-write-command
   (state <s> ^name op_write1
              ^problem-space.execution-type external
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-write-command
        ^object.handle <h>
        ^value <val>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name write
         ^object <h>
         ^value <val>)
   (<seg> ^write-command <pa>)
}

