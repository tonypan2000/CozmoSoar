### send the low-level go-to command to the output-link when the until-clause is satisfied ###

sp {op_go-to-xy1*propose*send-go-to-command
   (state <s> ^name op_go-to-xy1)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-go-to-command
        ^item-type command-operator
        ^command-name go-to-command)
}


### cozmo ###

sp {op_go-to-xy1*external*apply*send-go-to-command*cozmo
   (state <s> ^name op_go-to-xy1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^target-coord <coord>
              ^io.input-link.self.pose.yaw <yaw>
              ^top-state.io.output-link <out>)
   (<o> ^name send-go-to-command
        ^command-name <cmd-name>)
   (<coord> ^x <x> ^y <y>)
-->
   (<out> ^go-to-pose <gtp>)
   (<gtp> ^item-type output-command
          ^x <x>
          ^y <y>
          ^orientation <yaw>)
   (<seg> ^<cmd-name> <gtp>)
}

### magicbot ###

sp {op_go-to-xy1*external*apply*send-go-to-command*magicbot
   (state <s> ^name op_go-to-xy1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain magicbot
              ^current-task-segment <seg>
              ^target-coord <coord>
              ^top-state.io.output-link <out>)
   (<o> ^name send-go-to-command
        ^command-name <cmd-name>)
   (<coord> ^x <x> ^y <y>)
-->
    (<out> ^do-control-law <law>)
    (<law> ^item-type output-command
           ^name drive-xy
           ^parameters <params>
           ^termination-condition.name stabilized)
    (<params> ^x <x> ^y <y>)
   (<seg> ^<cmd-name> <law>)
}
