### Send the low-level approach command when the door is visible and reachable ###

sp {op_approach1*propose*send-approach-command
   (state <s> ^name op_approach1
              ^target-object <obj>)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^is-reachable1 reachable1
            ^arm-status not-grabbed)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-approach-command
        ^item-type command-operator
        ^command-name approach-command
        ^object <obj>)
}


### model send-approach-command ###

sp {op_approach1*internal*apply*send-approach-command*change*reachable
   (state <s> ^name op_approach1
              ^problem-space.execution-type internal
              ^operator <o>)
   (<o> ^name send-approach-command
        ^object.predicates <preds>)
   (<preds> ^is-reachable1 not-reachable1)
-->
   (<preds> ^is-reachable1 not-reachable1 -
            ^is-reachable1 reachable1)
}

sp {op_approach1*internal*apply*send-approach-command*change*visible
   (state <s> ^name op_approach1
              ^problem-space.execution-type internal
              ^operator <o>)
   (<o> ^name send-approach-command
        ^object.predicates <preds>)
   (<preds> ^is-visible1 not-visible1)
-->
   (<preds> ^is-visible1 not-visible1 -
            ^is-visible1 visible1)
}


### ai2thor ###

sp {op_approach1*external*apply*send-approach-command*ai2thor
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^agent-params.domain ai2thor
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-approach-command
        ^command-name <cmd-name>
        ^object.handle <h>)
-->
   (<ol> ^perform-action <pa>)
   (<pa> ^item-type output-command
         ^name approach
         ^object <h>)
   (<seg> ^<cmd-name> <pa>)
}


### cozmo ###

sp {op_approach1*external*apply*send-approach-command*cozmo
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^agent-params.domain cozmo
              ^operator <o>
              ^io.output-link <ol>
              ^current-task-segment <seg>)
   (<o> ^name send-approach-command
        ^command-name <cmd-name>
        ^object.handle <h>)
-->
   (<ol> ^go-to-object <gto>)
   (<gto> ^item-type output-command
          ^object-id <h>)
   (<seg> ^<cmd-name> <gto>)
}

