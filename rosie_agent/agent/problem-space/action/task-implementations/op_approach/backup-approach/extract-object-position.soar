# Ongoing svs filter extracting the object's position
sp {op_approach1*magicbot*elaborate*svs-filter*node_position*object
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^target-object.svs-obj.id <obj-id>
              ^top-state.svs.command <cmd>)
-->
    (<cmd> ^extract <pos>)
    (<pos> ^type node_position
            ^a <obj>)
    (<obj> ^type node
           ^id <obj-id>)
    (<s> ^object-position-filter <pos>)
}

sp {op_approach1*magicbot*elaborate*object-position*from*filter
   (state <s> ^name op_approach1
              ^object-position-filter.result.record.value <p>)
-->
    (<s> ^object-position <p>)
}

# Ongoing svs filter extracting the robot's position
sp {op_approach1*elaborate*svs-filter*node_position*robot
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^top-state.svs.command <cmd>)
-->
    (<cmd> ^extract <pos>)
    (<pos> ^type node_position
            ^a <obj>)
    (<obj> ^type node
           ^id robot)
    (<s> ^robot-position-filter <pos>)
}

sp {op_approach1*elaborate*robot-position*from*filter
   (state <s> ^name op_approach1
              ^robot-position-filter.result.record.value <p>)
-->
    (<s> ^robot-position <p>)
}

 


          
