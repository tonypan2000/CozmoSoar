###############################################################################
#
# Inside the execute-approach1 substate, this will put a orient command on the output link
#
###############################################################################

sp {op_approach1*propose*send-orient-command
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^object-position <pos>
              ^calculations.theta <yaw>
             -^current-task-segment.orient-command)
-->
   (<s> ^operator <o> + )
   (<o> ^name send-orient-command
        ^yaw <yaw>)
}

sp {op_approach1*apply*send-orient-command
   (state <s> ^name op_approach1
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-orient-command
        ^yaw <yaw>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^item-type output-command
          ^name orient
          ^parameters.yaw <yaw>
          ^termination-condition.name stabilized)
   (<seg> ^orient-command <law>)
}
