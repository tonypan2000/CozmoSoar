sp {op_approach1*propose*send-drive-to-xy-command
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^target-object <object>
              ^within-range false
              ^calculations.drive-x <x>
              ^calculations.drive-y <y>
              ^current-task-segment.orient-command.status success
              -^current-task-segment.drive-command)
-->
   (<s> ^operator <o> + )
   (<o> ^name send-drive-to-xy-command
        ^position <pos>)
   (<pos> ^x <x> ^y <y>)
}

sp {op_approach1*apply*send-drive-to-xy-command*send
   (state <s> ^name op_approach1
              ^operator <o>
              ^io.output-link <out>
              ^current-task-segment <seg>)
   (<o> ^name send-drive-to-xy-command
        ^position <pos>)
   (<pos> ^x <x> ^y <y>)
-->
    (<out> ^do-control-law <law>)
    (<law> ^item-type output-command
           ^name drive-xy
           ^parameters <params>
           ^termination-condition.name stabilized)
    (<params> ^x <x> ^y <y>)
   (<seg> ^drive-command <law>)
}
