### For the internal environment, simulate the approach command on the top-state world ###

sp {op_approach1*propose*simulate-approach-command
   (state <s> ^name op_approach1
              ^problem-space.execution-type external
              ^agent-params.domain internal
              ^current-task-segment <seg>)
   (<seg> ^approach-command <cmd>)
   (<cmd> -^status complete)
-->
   (<s> ^operator <o> +)
   (<o> ^name simulate-approach-command
        ^item-type command-operator
        ^command <cmd>)
}

sp {op_approach1*apply*simulate-approach-command*mark*command*complete
   (state <s> ^name op_approach1
              ^operator <o>)
   (<o> ^name simulate-approach-command
        ^command <cmd>)
-->
   (<cmd> ^status complete)
}

sp {op_approach1*apply*simulate-approach-command*change*reachable
   (state <s> ^name op_approach1
              ^operator.name simulate-approach-command
              ^target-object.predicates <preds>)
   (<preds> ^is-reachable1 not-reachable1)
-->
   (<preds> ^is-reachable1 not-reachable1 -
            ^is-reachable1 reachable1)
}

sp {op_approach1*apply*simulate-approach-command*change*visible
   (state <s> ^name op_approach1
              ^operator.name simulate-approach-command
              ^target-object.predicates <preds>)
   (<preds> ^is-visible1 not-visible1)
-->
   (<preds> ^is-visible1 not-visible1 -
            ^is-visible1 visible1)
}

