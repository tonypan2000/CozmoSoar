### send the low-level drive command to the output-link when the until-clause is satisfied ###

sp {op_drive1*propose*send-drive-command
   (state <s> ^name op_drive1
             -^current-task-segment.stop-command)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-drive-command
        ^item-type command-operator
        ^command-name drive-command)
}


### magicbot ###

sp {op_drive1*external*apply*send-drive-command*magicbot
   (state <s> ^name op_drive1
              ^problem-space.execution-type external
              ^agent-params.domain magicbot
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-drive-command
        ^command-name <cmd-name>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^item-type output-command
          ^name drive-forward
          ^termination-condition.name stabilized)
   (<seg> ^<cmd-name> <law>)
}


### ai2thor ###

sp {op_drive1*apply*send-drive-command*ai2thor
   (state <s> ^name op_drive1
              ^problem-space.execution-type external
              ^agent-params.domain ai2thor
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-drive-command
        ^command-name <cmd-name>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^name drive-forward)
   (<seg> ^<cmd-name> <pa>)
}


### cozmo ###

sp {op_drive1*external*apply*send-drive-command*cozmo
   (state <s> ^name op_drive1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-drive-command
        ^command-name <cmd-name>)
-->
   (<out> ^drive-forward <df>)
   (<df> ^item-type output-command
         ^distance 0.3
         ^speed 0.1)
   (<seg> ^<cmd-name> <df>)
}
