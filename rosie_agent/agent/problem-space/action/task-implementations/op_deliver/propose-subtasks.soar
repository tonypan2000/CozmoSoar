
sp {op_deliver1*external*propose*op_find1
   (state <s> ^name op_deliver1
              ^problem-space.execution-type external
              ^target-object <obj>
             -^current-task-segment.completed-approach true)
   (<obj> ^predicates.is-visible1 not-visible1
          -^predicates.arm-status grabbed)
-->
   (<s> ^operator <o> + =)
   (<o> ^name op_find1
        ^item-type task-operator
        ^task-handle find1
        ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <obj>)
}

sp {op_deliver1*external*propose*op_pick-up1
   (state <s> ^name op_deliver1
              ^problem-space.execution-type external
             -^current-task-segment.completed-pickup
              ^target-object <obj>
             -^current-task-segment.completed-approach true)
   (<obj> ^predicates <preds>)
   (<preds> ^is-visible1 visible1
            ^arm-status not-grabbed)
-->
   (<s> ^operator <o> + =)
   (<o> ^name op_pick-up1
        ^item-type task-operator
        ^task-handle pick-up1
        ^arg1 <arg1>)
   (<arg1> ^arg-type object
           ^id <obj>)
}

sp {op_deliver1*external*propose*op_go-to-location
   (state <s> ^name op_deliver1
              ^problem-space.execution-type external
              ^corner-location-info.parent-location <parent>
              ^world.robot.current-location.handle <> <loch>
              ^target-object.predicates.arm-status grabbed)
   (<parent> ^handle <loch>
             ^name <loc-name>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name op_go-to-location1
        ^item-type task-operator
        ^task-handle go-to-location1
        ^arg2 <arg2>)
   (<arg2> ^arg-type partial-predicate
           ^handle to1
           ^2 <loc>)
   (<loc> ^handle <loch>
          ^item-type object
          ^predicates <preds>)
   (<preds> ^category location
            ^name <loc-name>)
}


sp {op_deliver1*external*propose*op_approach1*coord
   (state <s> ^name op_deliver1
              ^problem-space.execution-type external
              ^corner-location-info <corner>
              ^world.robot.current-location.handle <loch>
              ^target-object.predicates.arm-status grabbed
             -^current-task-segment.completed-approach)
   (<corner> ^x <x> ^y <y> 
             ^parent-location.handle <loch>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name op_approach1
        ^item-type task-operator
        ^task-handle approach1
        ^arg1 <arg1>)
   (<arg1> ^arg-type coordinate
           ^coord <c>)
   (<c> ^x <x> ^y <y>)
}

sp {op_deliver1*external*propose*op_put-down1
   (state <s> ^name op_deliver1
              ^problem-space.execution-type external
              ^current-task-segment.completed-approach true
              ^target-object <obj>
              ^world.robot.current-location <loc>)
   (<obj> ^predicates.arm-status grabbed)
-->
   (<s> ^operator <o> + =)
   (<o> ^name op_put-down1
        ^item-type task-operator
        ^task-handle put-down1
        ^arg1 <arg1>
        ^arg2 <arg2>)
   (<arg1> ^arg-type object
           ^id <obj>)
   (<arg2> ^arg-type partial-predicate
           ^handle in1
           ^2 <loc>)
}

sp {deliver*SNC*interrupt
   (state <s> ^superstate.name op_deliver1
              ^operator.name search-for-next-subtask)
-->
   (interrupt)
}
