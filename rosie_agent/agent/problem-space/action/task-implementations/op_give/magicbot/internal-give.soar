sp {op_give*propose*internal-give
   (state <s> ^name op_give1
              ^held-object <obj>
              ^person <person>
              ^top-state.world.robot.arm.state internal
              -^current-task-segment.put-down-command)
-->
   (<s> ^operator <o> + >)
   (<o> ^name internal-give
        ^object <obj>
        ^person <person>)
}

sp {op_give*apply*internal-give*send-confirmation
   (state <s> ^name op_give1
              ^operator <o>
              ^current-task-segment.received-yes true
              ^top-state.interaction.changes <c>
              ^agent-params.arm-simulation-level none)
   (<o> ^name internal-give)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type confirm-give
          ^complete-message true)
}

sp {op_give*apply*internal-give*send-message
   (state <s> ^name op_give1
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.interaction.changes <c>)
   (<o> ^name internal-give
        ^object <obj>
        ^person <person>)
   (<seg> -^sent-message true)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type give-item-request
          ^item <obj>
          ^person <person>)
   (<seg> ^sent-message true)
}

sp {op_give*apply*internal-give*remove*grabbed
   (state <s> ^name op_give1
              ^operator.name internal-give
              ^held-object.predicates <preds>
              ^current-task-segment <seg>)
   (<seg> ^received-yes true)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -
            ^arm-status not-grabbed)
   (<seg> ^put-down-command.status success)
}


