sp {op_give1*elaborate*svs-filter*node_position*person
   (state <s> ^name op_give1
              ^person.svs-obj.id <obj-id>
              ^top-state.svs.command <cmd>
              ^world.robot.arm.state external)
-->
    (<cmd> ^extract <pos>)
    (<pos> ^type node_position
            ^a <obj>)
    (<obj> ^type node
           ^id <obj-id>)
    (<s> ^person-position-filter <pos>)
}

sp {op_give1*elaborate*person-position*from*filter
   (state <s> ^name op_give1
              ^person-position-filter.result.record.value <p>)
-->
    (<s> ^person-position <p>)
}

sp {op_give1*elaborate*svs-filter*node_position*robot
   (state <s> ^name op_give1
              ^top-state.svs.command <cmd>
              ^world.robot.arm.state external)
-->
    (<cmd> ^extract <pos>)
    (<pos> ^type node_position
            ^a <obj>)
    (<obj> ^type node
           ^id robot)
    (<s> ^robot-position-filter <pos>)
}

sp {op_give1*elaborate*robot-position*from*filter
   (state <s> ^name op_give1
               ^robot-position-filter.result.record.value <p>)
-->
    (<s> ^robot-position <p>)
}


sp {op_give1*elaborate*distance-to-person
   (state <s> ^name op_give1
              ^robot-position <rpos>
              ^person-position <opos>)
   (<rpos> ^x <rx> ^y <ry>)
   (<opos> ^x <ox> ^y <oy>)
-->
   (<s> ^distance-to-person (sqrt (+ (* (- <rx> <ox>) (- <rx> <ox>)) 
                                     (* (- <ry> <oy>) (- <ry> <oy>)))))
}

 


          
