sp {op_give1*internal*propose*perform-give
   (state <s> ^name op_give1
              ^problem-space.execution-type external
              ^target-object.predicates.arm-status grabbed)
-->
   (<s> ^operator <o> +)
   (<o> ^name perform-give)
}

sp {op_give1*internal*apply*perform-give*arm-status*not-grabbed
   (state <s> ^name op_give1
              ^problem-space.execution-type external
              ^operator.name perform-give
              ^target-object.predicates <preds>)
   (<preds> ^arm-status grabbed)
-->
   (<preds> ^arm-status grabbed -
            ^arm-status not-grabbed)
}

sp {op_give1*internal*apply*perform-give*add*holding*predicate
   (state <s> ^name op_give1
              ^problem-space.execution-type external
              ^operator.name perform-give
              ^target-object <obj>
              ^person <person>
              ^world.predicates.predicate <holding>)
   (<holding> ^handle holding1)
 -{(<holding> ^instance <i>)
   (<i> ^1 <person> ^2 <obj>)}
-->
   (<holding> ^instance <i>)
   (<i> ^1 <person> ^2 <obj>)
}
