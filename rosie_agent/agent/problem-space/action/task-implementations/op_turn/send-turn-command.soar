### send the low-level turn command to the output-link ###

sp {op_turn1*propose*send-turn-command
   (state <s> ^name op_turn1
              ^direction-id <dir>)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-turn-command
        ^item-type command-operator
        ^command-name turn-command
        ^direction <dir>)
}


### magicbot ###

sp {op_turn1*external*apply*send-turn-command*magicbot
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain magicbot
              ^current-task-segment <seg>
              ^top-state.perception-monitor.input-link.self.pose.yaw <yaw>
              ^top-state.io.output-link <out>)
   (<o> ^name send-turn-command
        ^command-name <cmd-name>
        ^direction <dir>)
   (<dir> ^side <side>
          ^yaw <dy>)
-->
   (<out> ^do-control-law <law>)
   (<law> ^item-type output-command
          ^name turn
          ^parameters.yaw (+ <yaw> <dy>)
          ^termination-condition.name stabilized)
   (<seg> ^<cmd-name> <law>)
}


### ai2thor ###

sp {op_turn1*external*apply*send-turn-command*ai2thor
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain ai2thor
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-turn-command
        ^command-name <cmd-name>
        ^direction.handle <dir>)
-->
   (<out> ^perform-action <pa>)
   (<pa> ^name turn
         ^direction <dir>)
   (<seg> ^<cmd-name> <pa>)
}


### cozmo ###

sp {op_turn1*external*apply*send-turn-command*cozmo
   (state <s> ^name op_turn1
              ^problem-space.execution-type external
              ^operator <o>
              ^agent-params.domain cozmo
              ^current-task-segment <seg>
              ^top-state.io.output-link <out>)
   (<o> ^name send-turn-command
        ^command-name <cmd-name>
        ^direction.yaw <yaw>)
-->
   (<out> ^turn-in-place <tip>)
   (<tip> ^item-type output-command
          ^angle <yaw>
          ^speed 1.57)
   (<seg> ^<cmd-name> <tip>)
}
