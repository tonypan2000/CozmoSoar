### Ask the person to start following the robot ###

sp {op_start-leading1*propose*send-message
   (state <s> ^name op_start-leading1
              ^current-task-segment <seg>
              ^person <person>)
   (<seg> -^sent-message)
-->
   (<s> ^operator <o> + =)
   (<o> ^name send-message
        ^item-type command-operator
        ^person <person>)
}


### internal send-message (during planning) ###

sp {op_start-leading1*internal*apply*send-message*mark*received-yes
   (state <s> ^name op_start-leading1
              ^problem-space.execution-type internal
              ^operator.name send-message
              ^current-task-segment <seg>)
-->
   (<seg> ^received-yes true
          ^sent-message true)
}


### external send-message ###

sp {op_start-leading1*external*apply*send-message*add*to*interaction*stack
   (state <s> ^name op_start-leading1
              ^problem-space.execution-type external
              ^operator <o>
              ^current-task-segment <seg>
              ^top-state.interaction.changes <c>)
   (<o> ^name send-message
        ^person.predicates.name <name>)
-->
   (<c> ^outgoing-message <msg>)
   (<msg> ^type start-leading-request
          ^person <name>)
   (<seg> ^sent-message true)
}
