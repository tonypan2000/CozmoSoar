sp {op_ask*propose*ask-message
   (state <s> ^name op_ask1
              ^problem-space.execution-type external
             -^current-task-segment.answer
              ^sentence <msg>)
-->
   (<s> ^operator <o> +)
   (<o> ^name ask-message
        ^message <msg>)
}

sp {op_ask*apply*ask-message*send*message
   (state <s> ^name op_ask1
              ^operator <o>
              ^top-state.interaction.changes <changes>)
   (<o> ^name ask-message
        ^message <msg>)
-->
   (<changes> ^outgoing-message <send>)
   (<send> ^type say-sentence
           ^complete-message true
           ^fields.sentence <msg>)
}

sp {op_ask*apply*ask-message*push*segment*wait-for-answer
   (state <s> ^name op_ask1
              ^operator <o>
              ^top-state.interaction.changes <changes>)
   (<o> ^name ask-message
        ^message <msg>)
-->
   (<changes> ^new-segment-info <info>)
   (<info> ^purpose <p>
   	   ^originator agent)
   (<p> ^type wait-for-answer
   	    ^satisfaction.dialog-event <de>)
   (<de> ^type object-description
         ^type object-definition
         ^type quoted-sentence
         ^type yes no
         ^originator instructor)
}
