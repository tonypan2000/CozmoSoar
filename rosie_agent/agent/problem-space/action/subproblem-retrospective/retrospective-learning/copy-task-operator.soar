# copy the task-operator for the task we're trying to learn 
sp {retrospective-learning*elaborate*copy-task-operator*for*subtask
   (state <s> ^name retrospective-learning
              ^problem-space.world <world>
              ^subtasks-stack.next.episode.task-stack.top.prev.task-operator <task-op>
              ^deep-copy-world.copy-objects-map <map>
             -^replayed-all-subtasks true)
-->
   (<s> ^copy-task-operator <cto>)
   (<cto> ^task-operator <task-op>
          ^destination <s>
          ^copy-name task-operator
          ^object-map <map>)
}

# copy the task-operator from the initial episode
sp {retrospective-learning*elaborate*copy-task-operator*initial*task-operator
   (state <s> ^name retrospective-learning
              ^problem-space.world <world>
              ^initial-episode.task-stack.top.task-operator <task-op>
              ^deep-copy-world.copy-objects-map <map>
              ^replayed-all-subtasks true)
-->
   (<s> ^copy-task-operator <cto>)
   (<cto> ^task-operator <task-op>
          ^destination <s>
          ^copy-name task-operator
          ^object-map <map>)
}

# copy each subtask retrieved from episodic memory
sp {retrospective-learning*elaborate*copy-task-operator*subtask
   (state <s> ^name retrospective-learning
              ^subtasks-stack.segments.segment <seg>)
   (<seg> ^episode <ep>)
   (<ep> ^task-stack.top.task-operator <sub>)
-->
   (<s> ^copy-task-operator <cto>)
   (<cto> ^task-operator <sub>
          ^episode <ep>
          ^destination <seg>
          ^copy-name subtask-copy
          ^object-map <map>)
}

sp {retrospective-learning*elaborate*copy-task-operator*subtask*object-map*entry
   (state <s> ^name retrospective-learning
              ^copy-task-operator <cto>
              ^world.objects.object <copy-obj>)
   (<cto> ^copy-name subtask-copy
          ^episode.world.objects.object <src-obj>
          ^object-map <map>)
   (<src-obj> ^handle <h>)
   (<copy-obj> ^handle <h>)
-->
   (<map> ^<src-obj> <copy-obj>)
}

#
#sp {retrospective-learning*apply*copy-task-operator*copy*merged*info
#   (state <s> ^name retrospective-learning
#              ^operator <o>
#              ^task-operator <operator>)
#   (<o> ^name copy-task-operator
#        ^task-operator.merged <m>)
#   (<m> ^object1.handle <handle1>
#        ^object2.handle <handle2>)
#-->
#   (<operator> ^merged.<handle1> <handle2>)
#}
