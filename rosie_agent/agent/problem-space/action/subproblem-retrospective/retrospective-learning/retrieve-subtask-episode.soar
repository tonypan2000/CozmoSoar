sp {retrospective-learning*propose*retrieve-subtask-episode*first
   (state <s> ^name retrospective-learning
              ^initial-episode <ep>
              ^subtasks-stack <subs>)
   (<subs> ^max-episode-id <ep-id>
           ^next none)
-->
   (<s> ^operator <o> +)
   (<o> ^name retrieve-subtask-episode
        ^after-episode-id <ep-id>)
}

sp {retrospective-learning*propose*retrieve-subtask-episode*next
   (state <s> ^name retrospective-learning
              ^initial-episode <ep>
              ^subtasks-stack <subs>)
   (<subs> ^max-episode-id <ep-id>
           ^next <top>
          -^retrieved-all true)
   (<top> ^learning-result success)
-->
   (<s> ^operator <o> +)
   (<o> ^name retrieve-subtask-episode
        ^after-episode-id <ep-id>)
}

sp {retrospective-learning*apply*retrieve-subtask-episode*create*cue
   (state <s> ^name retrospective-learning
              ^operator <o>
              ^epmem.command <cmd>
              ^subtasks-stack <subs>
              ^task-handle <handle>)
   (<o> ^name retrieve-subtask-episode)
   (<subs> ^max-episode-id <max-id>
           ^min-episode-id <min-id>)
-->
   (<cmd> ^query <q>
          ^after <min-id>
          ^before <max-id>)
   (<q> ^task-stack.top <top>)
   (<top> ^start-of-execution true
          ^prev.task-operator.task-handle <handle>)
}

sp {retrospective-learning*apply*retrieve-subtask-episode*success
   (state <s> ^name retrospective-learning
              ^operator.name retrieve-subtask-episode
              ^subtasks-stack <subs>
              ^epmem <epmem>)
   (<epmem> ^result <res>
            ^result.retrieved <episode>
            ^command <cmd>)
   (<res> ^success <q>
          ^retrieved <episode>
          ^memory-id <ep-id>
          ^normalized-match-score 1.0)
   (<cmd> ^query <q>)
   (<subs> ^max-episode-id <old-max>
           ^next <next>)
-->
   (<subs> ^max-episode-id <old-max> -
           ^max-episode-id (- <ep-id> 5) # Subtract 5 just to make sure it's before the opearator is first selected
           ^next <next> -
           ^next <new-seg>)
   (<new-seg> ^episode <episode>
              ^episode-id <ep-id>
              ^end-episode-id <old-max>
              ^next <next>)
   (<cmd> ^query <q> -)
}

sp {retrospective-learning*apply*retrieve-subtask-episode*failure*no*perfect*match
   (state <s> ^name retrospective-learning
              ^operator.name retrieve-subtask-episode
              ^epmem <epmem>
              ^subtasks-stack <subs>)
   (<epmem> ^result.normalized-match-score < 1.0
            ^command <cmd>)
   (<cmd> ^query <q>)
-->
   (<subs> ^retrieved-all true)
   (<cmd> ^query <q> -)
}

sp {retrospective-learning*apply*retrieve-subtask-episode*failure*no*match
   (state <s> ^name retrospective-learning
              ^operator.name retrieve-subtask-episode
              ^epmem <epmem>
              ^subtasks-stack <subs>)
   (<epmem> ^result.failure
            ^command <cmd>)
   (<cmd> ^query <q>)
-->
   (<subs> ^retrieved-all true)
   (<cmd> ^query <q> -)
}
