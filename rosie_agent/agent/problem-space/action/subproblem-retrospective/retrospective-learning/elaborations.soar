# The current episode id
sp {retrospective-learning*elaborate*present-id
   (state <s> ^name retrospective-learning
              ^superstate.operator.present-id <id>)
-->
   (<s> ^present-id <id>)
}

# The task segment for the task we are trying to learn the policy for
sp {retrospective-learning*elaborate*current-task-segment
   (state <s> ^name retrospective-learning
              ^superstate.purpose.parameters.task-segment <task-segment>)
-->
   (<s> ^current-task-segment <task-segment>)
}

sp {retrospective-learning*elaborate*task-handle
   (state <s> ^name retrospective-learning
              ^current-task-segment.task-operator.task-handle <h>)
-->
   (<s> ^task-handle <h>)
}

#sp {retrospective-learning*elaborate*subtasks*answer
#   (state <s> ^name retrospective-learning
#              ^subtasks <subs>
#              ^current-task-segment.answer.handle <h>)
#-->
#  (<subs> ^answer <h>)
#}

#sp {retrospective-learning*elaborate*world*predicate*on*top-state
#   (state <s> ^name retrospective-learning
#              ^learning-context.episode.world.predicates <preds>
#              ^top-state.world.predicates.predicate <pred>
#              ^world.predicates <cur-preds>)
#   (<preds> -^predicate.handle <pred-handle>)
#   (<pred> ^handle <pred-handle>)
#-->
#   (<cur-preds> ^predicate <new-pred>)
#   (<new-pred> ^handle <pred-handle>)
#}

##############################################################
# subtasks-stack
# Contains a stack of the subtasks performed during the task
# Including the episodes where each was initiated
##############################################################

sp {retrospective-learning*elaborate*subtasks-stack
   (state <s> ^name retrospective-learning)
-->
   (<s> ^subtasks-stack <subs>)
   (<subs> ^next none
           ^segments <segs>)
}

# The subtasks structure used to copy subtasks into substates
# (Contains only the actions)
sp {retrospective-learning*elaborate*subtasks*top
   (state <s> ^name retrospective-learning
              ^subtasks-stack.next.subtask-copy <sub>)
-->
   (<s> ^subtasks <subs>)
   (<subs> ^next <sub>)
}

# Elaborate next pointers
sp {retrospective-learning*elaborate*subtasks*next*none
   (state <s> ^name retrospective-learning
              ^subtasks-stack.segments.segment <seg>)
   (<seg> ^subtask-copy <sub>
          ^next none)
-->
   (<sub> ^next none)
}

sp {retrospective-learning*elaborate*subtasks*next*sub
   (state <s> ^name retrospective-learning
              ^subtasks-stack.segments.segment <seg>)
   (<seg> ^subtask-copy <sub>
          ^next.subtask-copy <next>)
-->
   (<sub> ^next <next>)
}

# Elaborate the set of segments
sp {retrospective-learning*elaborate*subtasks-stack*segment
   (state <s> ^name retrospective-learning
              ^subtasks-stack <subs>)
   (<subs> ^next { <seg> <> none }
           ^segments <segs>)
-->
   (<segs> ^segment <seg>)
}

sp {retrospective-learning*elaborate*subtasks-stack*segment*next
   (state <s> ^name retrospective-learning
              ^subtasks-stack <subs>)
   (<subs> ^segments <segs>)
   (<segs> ^segment.next { <seg> <> none })
-->
   (<segs> ^segment <seg>)
}

# replayed-all-subtasks true
#   elaborated when all subtasks have been replayed 
sp {retrospective-learning*elaborate*replayed-all-subtasks
   (state <s> ^name retrospective-learning
              ^subtasks-stack.retrieved-all true)
-->
   (<s> ^replayed-all-subtasks true)
}
