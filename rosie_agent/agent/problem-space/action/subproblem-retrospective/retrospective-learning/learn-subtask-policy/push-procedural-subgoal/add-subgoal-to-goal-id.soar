sp {push-procedural-subgoal*propose*add-subgoal-to-goal-id
   (state <s> ^name push-procedural-subgoal
              ^task-concept-network.goal <g>
              ^subtask-handle <sub-h>
              -^added-subgoal <sub>)
   (<g> ^subgoal-count <c>)
-->
   (<s> ^operator <o> +)
   (<o> ^name add-subgoal-to-goal-id
        ^new-subgoal-count (+ <c> 1)
        ^subtask-handle <sub-h>)
}

sp {push-procedural-subgoal*apply*add-subgoal-to-goal-id*increment*other*goals
   (state <s> ^name push-procedural-subgoal
              ^operator.name add-subgoal-to-goal-id
              ^task-concept-network.goal <g>)
   (<g> ^{ <cur-num> << 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 >> } <other-subgoal>)
-->
   (<g> ^<cur-num> <other-subgoal> -
        ^(+ <cur-num> 1) <other-subgoal>)
}

sp {push-procedural-subgoal*apply*add-subgoal-to-goal-id
   (state <s> ^name push-procedural-subgoal
              ^operator <o>
              ^task-concept-network.goal <g>)
   (<o> ^name add-subgoal-to-goal-id
        ^new-subgoal-count <nc>
        ^subtask-handle <sub-h>)
   (<g> ^subgoal-count <old-c>)
-->
   (<g> ^subgoal-count <old-c> -
        ^subgoal-count <nc>
        ^1 <subgoal>)
   (<subgoal> ^handle (make-constant-symbol |task-subgoal|)
              ^predicates <preds>)
   (<preds>   ^pred-count 1
              ^1 <pred1>)
   (<pred1> ^type subtask
            ^subtask-handle <sub-h>)

   (<s> ^added-subgoal <subgoal>)
}

