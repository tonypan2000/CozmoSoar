##### operator create-operator-base
# creates the basic structure for the operator as task-operator

# propose create-operator-base
sp {task-utils*construct-task-operator*propose*create-operator-base
   (state <s> ^name construct-task-operator
              ^task-concept-network <tcn>
             -^task-operator.name)
-->
   (<s> ^operator <o> +)
   (<o> ^name create-operator-base
        ^task-concept-network <tcn>)
} 

# create the task-operator strucutre with task-handle
sp {task-utils*construct-task-operator*apply*create-operator-base
   (state <s> ^name construct-task-operator
              ^operator <o>)
   (<o> ^name create-operator-base
        ^task-concept-network <tcn>)
   (<tcn> ^handle <task-handle>)
-->
   (<s> ^task-operator <task-op>)
   (<task-op> ^item-type task-operator
              ^task-handle <task-handle>)
}

# Copy the name of the operator
sp {task-utils*construct-task-operator*apply*create-operator-base*copy*operator*name
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-operator <task-op>)
   (<o> ^name create-operator-base
        ^task-concept-network.procedural.op_name <name>)
-->
   (<task-op> ^name <name>)
}

# @TODO: Change to perform-once true/false? 
# Copy the perform flag
sp {task-utils*construct-task-operator*apply*create-operator-base*copy*perform
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-operator <task-op>)
   (<o> ^name create-operator-base
        ^task-concept-network.perform <perf>)
-->
   (<task-op> ^perform <perf>)
}

sp {task-utils*construct-task-operator*apply*create-operator-base*copy*perform*default
   (state <s> ^name construct-task-operator
              ^operator <o>
              ^task-operator <task-op>)
   (<o> ^name create-operator-base
        ^task-concept-network <tcn>)
   (<tcn> -^perform <perf>)
-->
   (<task-op> ^perform multiple)
}
