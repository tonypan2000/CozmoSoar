#### constructing object arguments


# If it is not an smem object, it is already in the proper representation
sp {task-utils*construct-task-argument*elaborate*construct-task-argument*object*world-object*not*smem*not*linked
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type object
          ^source <src-obj>)
   (<src-obj> ^item-type object
             -^object-instance true
             -^linked-to)
-->
   (<arg> ^world-object <src-obj>)
}

sp {task-utils*construct-task-argument*elaborate*construct-task-argument*object*world-object*not*smem*linked
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type object
          ^source <src-obj>)
   (<src-obj> ^item-type object
             -^object-instance true
             ^linked-to <other>)
-->
   (<arg> ^world-object <other>)
}

# If the object is an smem object, create a world reprensetation for it

### Implemented in manage-world-state/world-utils/construct-world-object ###
sp {task-utils*construct-task-argument*propose*construct-world-object*from*smem
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type object
          ^source <src-obj>
         -^world-object)
   (<src-obj> ^object-instance true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-world-object
        ^source-type smem-obj
        ^source-obj <src-obj>
        ^destination <arg>
        ^result-name world-object)
}

### construct-task-argument object

sp {task-utils*construct-task-argument*propose*construct-task-argument*object
   (state <s> ^construct-task-argument <arg>)
   (<arg> ^arg-type object
          ^world-object <obj>
         -^created-arg)
-->
   (<s> ^operator <o> + =)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type object
        ^object <obj>)
}

sp {task-utils*construct-task-argument*apply*construct-task-argument*object
   (state <s> ^operator <o>)
   (<o> ^name construct-task-argument
        ^argument <arg>
        ^arg-type object
        ^object <obj>)
-->
   (<arg> ^created-arg <carg>)
   (<carg> ^id <obj>)
}


