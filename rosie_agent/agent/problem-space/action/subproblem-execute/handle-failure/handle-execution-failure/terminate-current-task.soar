sp {handle-execution-failure*propose*terminate-current-task
   (state <s> ^name handle-execution-failure
              ^task-segment <seg>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name terminate-current-task)
}

sp {handle-execution-failure*apply*terminate-current-task*add*failure*status
   (state <s> ^name handle-execution-failure
              ^operator.name terminate-current-task
              ^task-segment <seg>)
-->
   (<seg> ^unrecoverable-failure execution-failure)
}

sp {handle-execution-failure*apply*terminate-current-task*send*message*execution-failure
   (state <s> ^name handle-execution-failure
              ^operator.name terminate-current-task
             -^silent true
              ^task-segment.task-operator.task-handle <task-handle>
             -^failure-subtype unsatisfied-goal
              ^failed-command.name <cmd-name>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type execution-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^task-handle <task-handle>
        ^command-name <cmd-name>)
}

sp {handle-execution-failure*apply*terminate-current-task*send*message*unsatisfied-goal
   (state <s> ^name handle-execution-failure
              ^operator.name terminate-current-task
             -^silent true
              ^task-segment.task-operator.task-handle <task-handle>
              ^failure-subtype unsatisfied-goal
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type unsatisfied-goal
          ^complete-message true
          ^fields <f>)
   (<f> ^task-handle <task-handle>)
}
