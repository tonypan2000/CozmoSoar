
sp {handle-invalid-argument-failure*propose*terminate-current-task
   (state <s> ^name handle-invalid-argument-failure
              ^task-segment <seg>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name terminate-current-task)
}

sp {handle-invalid-argument-failure*apply*terminate-current-task*add*failure*status
   (state <s> ^name handle-invalid-argument-failure
              ^operator.name terminate-current-task
              ^task-segment <seg>)
-->
   (<seg> ^unrecoverable-failure invalid-argument-failure)
}

sp {handle-invalid-argument-failure*apply*terminate-current-task*send*message*invalid-argument-failure*missing-argument
   (state <s> ^name handle-invalid-argument-failure
              ^operator.name terminate-current-task
             -^silent true
              ^task-segment.task-operator.task-handle <task-handle>
              ^missing-argument <arg-name>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type invalid-argument-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^subtype missing-argument
        ^task-handle <task-handle>
        ^missing-argument <arg-name>)
}

sp {handle-invalid-argument-failure*apply*terminate-current-task*send*message*invalid-argument-failure*missing-property
   (state <s> ^name handle-invalid-argument-failure
              ^operator.name terminate-current-task
             -^silent true
              ^task-segment.task-operator.task-handle <task-handle>
              ^missing-property <prop-name>
              ^invalid-argument.id <obj>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type invalid-argument-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^subtype missing-property
        ^task-handle <task-handle>
        ^object <obj>
        ^missing-property <prop-name>)
}

sp {handle-invalid-argument-failure*apply*terminate-current-task*send*message*invalid-argument-failure*default
   (state <s> ^name handle-invalid-argument-failure
              ^operator.name terminate-current-task
             -^silent true
              ^task-segment.invalid-argument-operator.task-handle <task-handle>
              -^missing-property
              ^argument-name <arg-name>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type invalid-argument-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^subtype default
        ^task-handle <task-handle>
        ^argument-name <arg-name>)
}
