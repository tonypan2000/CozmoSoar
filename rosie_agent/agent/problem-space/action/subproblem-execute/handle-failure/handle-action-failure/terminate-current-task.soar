sp {handle-action-failure*propose*terminate-current-task
   (state <s> ^name handle-action-failure
              ^task-segment <seg>)
-->
   (<s> ^operator <o> + <)
   (<o> ^name terminate-current-task)
}

sp {handle-action-failure*apply*terminate-current-task*add*failure*status
   (state <s> ^name handle-action-failure
              ^operator.name terminate-current-task
              ^task-segment <seg>)
-->
   (<seg> ^unrecoverable-failure action-failure)
}

sp {handle-action-failure*apply*terminate-current-task*send*message*action-failure
   (state <s> ^name handle-action-failure
              ^operator.name terminate-current-task
             -^silent true
              ^task-segment.task-operator.task-handle <task-handle>
              ^failed-action-name <name>
              ^top-state.interaction.changes <chgs>)
-->
   (<chgs> ^outgoing-message <msg>)
   (<msg> ^type action-failure
          ^complete-message true
          ^fields <f>)
   (<f> ^task-handle <task-handle>
        ^action-name <name>)
}
