sp {subproblem*execute*propose*advance-current-subgoal
   (state <s> ^problem-space.subproblem execute
              ^task-concept-network.goal <g>
              ^task-operator <task-op>
              ^desired <des>)
   (<g> ^subgoal-count <c>
        ^{ <sg-num> < <c> }.handle <subgoal-h>)
   (<des> ^handle <subgoal-h>
          ^satisfied true)
   (<task-op> ^current-subgoal <subgoal-h>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name advance-current-subgoal
        ^next-subgoal-num (+ <sg-num> 1))
}

sp {subproblem*execute*propose*advance-current-subgoal*unsatisfied*conditional*action
   (state <s> ^problem-space.subproblem execute
              ^evaluate-subtask-conditions.instantiation <i>
              ^task-concept-network.goal <g>
              ^task-operator <task-op>)
   (<g> ^subgoal-count <c>
        ^{ <sg-num> < <c> }.handle <subgoal-h>)
   (<task-op> ^current-subgoal <subgoal-h>)
   (<i> -^satisfied true)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name advance-current-subgoal
        ^next-subgoal-num (+ <sg-num> 1))
}

sp {subproblem*execute*apply*advance-current-subgoal*change*current-subgoal
   (state <s> ^problem-space.subproblem execute
              ^operator <o>
              ^task-operator <task-op>
              ^task-concept-network.goal <g>)
   (<o> ^name advance-current-subgoal
        ^next-subgoal-num <next-sg-num>)
   (<task-op> ^current-subgoal <old-sg>)
   (<g> ^<next-sg-num> <new-subgoal>)
   (<new-subgoal> ^handle <new-sg>)
-->
   (<task-op> ^current-subgoal <old-sg> -
              ^current-subgoal <new-sg>)
}

sp {subproblem*execute*apply*advance-current-subgoal*remove*performed-subtask
   (state <s> ^problem-space.subproblem execute
              ^operator.name advance-current-subgoal
              ^task-operator <task-op>)
   (<task-op> ^performed <sub-h>)
-->
   (<task-op> ^performed <sub-h> -)
}


