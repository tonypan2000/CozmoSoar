sp {subproblem*execute*propose*add-subtask-to-tcn*task-source*instruction
   (state <s> ^problem-space.subproblem execute
              ^child-task-segment <seg>
              ^task-operator <task-op>)
   (<seg> ^task-source instruction
          ^task-operator <subtask>)
-->
   (<s> ^operator <o> + > =)
   (<o> ^name add-subtask-to-tcn
        ^subtask <subtask>
        ^task-operator <task-op>)
}

sp {subproblem*execute*apply*add-subtask-to-tcn*finished*remove*task-source
   (state <s> ^problem-space.subproblem execute
              ^operator.name add-subtask-to-tcn
              ^child-task-segment <child-seg>
              ^current-task-segment <seg>
              ^created-subtask <sub-h>)
   (<child-seg> ^task-source instruction)
-->
   (<child-seg> ^task-source instruction -)
   (<s> ^created-subtask <sub-h> -)
   (<seg> ^learn-subtask-proposal <sub-h>)
}
    
sp {subproblem*execute*apply*add-subtask-to-tcn*finished*add*subtask-handle
   (state <s> ^problem-space.subproblem execute
              ^operator.name add-subtask-to-tcn
              ^child-task-segment <seg>
              ^created-subtask <sub-h>)
   (<seg> ^task-source instruction
          ^task-operator <task-op>)
-->
   (<task-op> ^subtask-handle <sub-h>)
   (<seg> ^assigned-subtask-handle true)
}
    
